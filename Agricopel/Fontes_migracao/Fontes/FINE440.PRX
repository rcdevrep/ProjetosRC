#INCLUDE "FINE440.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "TOPCONN.CH"

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FINE440    ³ Autor ³ --------------------- ³ Data ³ --.--.--³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calculo e recalculo de comissoes espeficifico baseado no    ³±±
±±³          ³ FINA440. Deve ser executada apos o reprocessameto FINA440.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FINE440()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ AGRICOPEL                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³ DATA   ³ BOPS ³Prograd.³ALTERACAO                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³08.03.01³xxxxxx³Jeniffer³Calculo da Comiss. Vend. conforme imposto inci-³±±
±±³        ³      ³        ³de ou nao na nota.                             ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

*/
User Function FINE440()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local   nOpca := 0

LOCAL oDlg
Local aSays:={}, aButtons:={}

Private cCadastro := OemToAnsi(STR0003) //"C lculo de Comiss”es Off-Line"
Pergunte("AFI440",.f.)

AADD(aSays,OemToAnsi( STR0004 ) ) //"  Este programa tem como objetivo executar os c lculos das comiss”es dos"
AADD(aSays,OemToAnsi( STR0005 ) ) //"vendedores, conforme os parƒmetros definidos pelo usu rio.              "
AADD(aButtons, { 5,.T.,{|| Pergunte("AFI440",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca:= 1,o:oWnd:End()}} )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
FormBatch( cCadastro, aSays, aButtons )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Parƒmetros:                                                               ³
//³ mv_par01 = Data Inicial       ?                                          ³
//³ mv_par02 = Data Final         ?                                          ³
//³ mv_par03 = Do Vendedor        ?                                          ³
//³ mv_par04 = At‚ o Vendedor     ?                                          ³
//³ mv_par05 = Considera Juros    ? Sim/NÆo                                  ³
//³ mv_par06 = Considera Descontos? Sim/NÆo                                  ³
//³ mv_par07 = Calcula Comiss s/NCC? Sim/NÆo                                 ³
//³ mv_par08 = Calcular           ? Ambas/Emissao/Baixa   						  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nOpcA == 1)

	// MODIFICAR A ROTINA FE440DELE3 PARA APAGR
	
	Processa({|lEnd| fe440DelE3(MV_PAR08)},STR0007)  //"Excluindo Comiss”es n„o pagas"

	If ( MV_PAR08 <> 2 )
		Processa({|lEnd| Fe440ProcB()},STR0009) //"Calculando Comiss”es pela Baixa"
	Endif
EndIf

dbSelectArea("SE3")
dbSetOrder(1)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fe440DelE3   ³ Autor ³ --------------------- ³ Data ³--.--.--³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Zera as comissoes especificas do periodo                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINE440                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fe440DelE3(nTipo)

Local aArea		:= GetArea()
Local aAreaSE1	:= SE1->(GetArea())
Local cChave    := ""
Local cArqInd   := ""
Local cQuery	:= ""
Local nIndex    := 0
Local nX			:= 0
Local nMax		:= 0
Local nMin		:= 0

dbSelectArea("SE3")
dbSetOrder(1)
ProcRegua(RecCount())
#IFDEF TOP
	If ( TcSrvType()!="AS/400" )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica qual eh o maior e o menor Recno que satisfaca a selecao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
		cQuery +=	 "MAX(R_E_C_N_O_) MAXRECNO "
		cQuery += "FROM "+RetSqlName("SE3")+" SE3 "
		cQuery += "WHERE SE3.E3_FILIAL='"+xFilial("SE3")+"' AND "
		cQuery += 	"SE3.E3_EMISSAO>='"+Dtos(mv_par01)+"' AND "
		cQuery += 	"SE3.E3_EMISSAO<='"+Dtos(mv_par02)+"' AND "
		cQuery += 	"SE3.E3_VEND>='"+mv_par03+"' AND "
		cQuery += 	"SE3.E3_VEND<='"+mv_par04+"' AND "
		cQuery += 	"SE3.E3_DATA='"+Dtos(Ctod(""))+"' AND "
		cQuery += 	"SE3.E3_ORIGEM NOT IN(' ','L') AND "
		Do Case
		Case nTipo == 2
			cQuery += "SE3.E3_BAIEMI='E' AND "
		Case nTipo == 3
			cQuery += "SE3.E3_BAIEMI='B' AND "			
		EndCase
		cQuery += 	"SE3.D_E_L_E_T_<>'*'"
		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"FA440DELE3")

		nMax := FA440DELE3->MAXRECNO
		nMin := FA440DELE3->MINRECNO
		dbCloseArea()
		dbSelectArea("SE3")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta a string de execucao no banco³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cQuery := "DELETE FROM "+RetSqlName("SE3")+" "
		cQuery += "WHERE E3_FILIAL='"+xFilial("SE3")+"' AND "
		cQuery += "E3_EMISSAO>='"+Dtos(mv_par01)+"' AND "
		cQuery += "E3_EMISSAO<='"+Dtos(mv_par02)+"' AND "
		cQuery += "E3_VEND>='"+mv_par03+"' AND "
		cQuery += "E3_VEND<='"+mv_par04+"' AND "
		cQuery += "E3_DATA='"+Dtos(Ctod(""))+"' AND "
		cQuery += "E3_ORIGEM NOT IN(' ','L') AND "
		Do Case
		Case nTipo == 2
			cQuery += "E3_BAIEMI='E' AND "
		Case nTipo == 3
			cQuery += "E3_BAIEMI='B' AND "
		EndCase
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa a string de execucao no banco para os proximos 1024 registro a fim de nao estourar o log do SGBD³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nX := nMin To nMax STEP 1024
			cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+""
			TcSqlExec(cQuery+cChave)
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³A tabela eh fechada para restaurar o buffer da aplicacao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE3")
		dbCloseArea()
		ChkFile("SE3",.F.)
	Else
#ENDIF
	cQuery := 'E3_FILIAL=="'+xFilial("SE3")+'".And.'
	cQuery += 'Dtos(E3_EMISSAO)>="'+Dtos(mv_par01)+'".And.'
	cQuery += 'Dtos(E3_EMISSAO)<="'+Dtos(mv_par02)+'".And.'
	cQuery += 'E3_VEND>="'+mv_par03+'".And.'
	cQuery += 'E3_VEND<="'+mv_par04+'".And.'
	cQuery += 'Dtos(E3_DATA)=="'+Dtos(cTod(""))+'".And.'
	Do Case
	Case nTipo == 2
		cQuery += "E3_BAIEMI='E'.And."
	Case nTipo == 3
		cQuery += "E3_BAIEMI='B' .And."
	EndCase		
	cQuery += '!(E3_ORIGEM$" #L")'
	cArqInd  := CriaTrab(,.F.)
	cChave   := IndexKey()
	IndRegua("SE3",cArqInd,cChave,,cQuery,OemToAnsi(STR0006))  //"Selecionando Registros..."
	nIndex := RetIndex("SE3")
	dbSelectArea("SE3")
	#IFNDEF TOP
		dbSetIndex(cArqInd+OrdBagExt())
	#ENDIF
	dbSelectArea("SE3")
	dbSetOrder(nIndex+1)
	MsSeek(xFilial("SE3"),.T.)

	While ( ! Eof() .And. xFilial("SE3") == SE3->E3_FILIAL )

		DbSelectArea("SE1")
		DbSetOrder(1)
		DbGotop()
		If DbSeek(xFilial("SE1")+SE3->E3_PREFIXO+SE3->E3_NUM+SE3->E3_PARCELA+SE3->E3_TIPO)
			If !Empty(SE1->E1_NUMLIQ)
				RecLock("SE3",.F.)
				dbDelete()
				MsUnlock()			
			EndIf
		EndIf
		DbSelectArea("SE3")
		SE3->(DbSkip())
		IncProc()
	Enddo
	dbSelectArea("SE3")
	RetIndex("SE3")
	dbClearFilter()
	FErase(cArqInd+OrdBagExt())
	#IFDEF TOP
	EndIf
	#ENDIF
RestArea(aAreaSE1)
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FE440ProcB ³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa o calculo das comissoes pela Baixa.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINE440                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fe440ProcB()

Local aBaixas 	:= {}
Local aBxEst	:= {}
Local aStruSE5  := {}
Local cChave	:= ""
Local cArqInd   := ""
Local cQuebra   := ""
Local cQuery    := ""
Local cAliasSE5 := "SE5"
Local nIndex    := 0
Local nCntFor   := 0
Local cSeekSE5  := ""
Local lQuery    := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³             Array aBaixas             ³
//³                                       ³
//³ 1 : Motivo da Baixa                   ³
//³ 2 : Sequencia da Baixa                ³
//³ 3 : Registro no SE5                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre com outro alias para eliminar o filtro do Top Connect ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( ChkFile("SE5",.F.,"NEWSE5") )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Selecionando Registros.               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE5")
	ProcRegua(RecCount())
	cChave   := "E5_FILIAL+DTOS(E5_DATA)+E5_PREFIXO+E5_NUMERO+E5_PARCELA+E5_TIPO+E5_CLIFOR+E5_LOJA+E5_MOTBX+E5_SEQ"
	#IFDEF TOP
		cAliasSE5 := "FA440PROCB"
		lQuery    := .T.
		aStruSE5  := SE5->(dbStruct())
		
		cQuery := "SELECT SE5.*,SE5.R_E_C_N_O_ SE5RECNO "
		cQuery += "FROM "+RetSqlName("SE5") + " SE5 "
		cQuery += "WHERE "
		cQuery += Fe440ChecF(2,.T.)+" AND "
		cQuery += "D_E_L_E_T_=' ' "
		cQuery += "ORDER BY "+SqlOrder(cChave)
		
		cQuery := ChangeQuery(cQuery)
		
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE5)
		For nCntFor := 1 To Len(aStruSE5)
			If aStruSE5[nCntFor][2] <> "C"
				TcSetField(cAliasSE5,aStruSE5[nCntFor][1],aStruSE5[nCntFor][2],aStruSE5[nCntFor][3],aStruSE5[nCntFor][4])
		    EndIf
		Next nCntFor
	
	#ELSE
		cArqInd  := CriaTrab(,.F.)
		
		IndRegua("SE5",cArqInd,cChave,,Fe440ChecF(2),OemToAnsi(STR0006))  //"Selecionando Registros..."
		nIndex := RetIndex("SE5")
		dbSelectArea("SE5")
		dbSetIndex(cArqInd+OrdBagExt())
		dbSetOrder(nIndex+1)
		MsSeek(xFilial(),.T.)
	#ENDIF

	cQuebra := (cAliasSE5)->E5_PREFIXO+(cAliasSE5)->E5_NUMERO+(cAliasSE5)->E5_PARCELA+(cAliasSE5)->E5_TIPO+(cAliasSE5)->E5_CLIFOR+(cAliasSE5)->E5_LOJA

	While ( ! Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL )
		If !f440Loja("SE5",cAliasSE5)		// Baixas de Vendas do SIGALOJA nao deve entrar.
			If ( (cAliasSE5)->E5_TIPODOC != "ES" .and. !(cAliasSE5)->E5_TIPO $ MVPAGANT) ;
					.and. (mv_par07 == 1 .or. (mv_par07 == 2 .and. !(Substr(E5_DOCUMEN,11,3)=="NCC")))
					
				Aadd(aBaixas,{ (cAliasSE5)->E5_MOTBX,(cAliasSE5)->E5_SEQ,IIF(lQuery,(cAliasSE5)->SE5RECNO,(cAliasSE5)->(Recno())) })
				
			Elseif (cAliasSE5)->E5_TIPODOC == "ES"
				aadd(aBxEst, IIF(lQuery,(cAliasSE5)->SE5RECNO,(cAliasSE5)->(Recno())) )
			EndIf
		EndIf
		dbSelectArea(cAliasSE5)
		dbSkip()
		IncProc(STR0010+":"+(cAliasSE5)->E5_PREFIXO+(cAliasSE5)->E5_NUMERO+(cAliasSE5)->E5_PARCELA) //"Titulo"
		If (cQuebra != (cAliasSE5)->E5_PREFIXO+(cAliasSE5)->E5_NUMERO+(cAliasSE5)->E5_PARCELA+;
				(cAliasSE5)->E5_TIPO+(cAliasSE5)->E5_CLIFOR+(cAliasSE5)->E5_LOJA .Or. Eof())

			If ( !Empty(aBaixas) )
				u_fe440CalcB(aBaixas,If(MV_PAR05==1,.T.,.F.),If(MV_PAR06==1,.T.,.F.),"FINE440",,mv_par03,mv_par04)
			EndIf
			If ( !Empty(aBxEst) )
				For nCntFor := 1 To Len(aBxEst)
					aBaixas := {}
					dbSelectArea("NEWSE5")
					MsGoto(aBxEst[nCntFor])
					cSeekSE5 := NEWSE5->E5_PREFIXO+NEWSE5->E5_NUMERO+NEWSE5->E5_PARCELA+NEWSE5->E5_TIPO+NEWSE5->E5_CLIFOR+NEWSE5->E5_LOJA+NEWSE5->E5_SEQ
					dbSelectArea("NEWSE5")
					dbSetOrder(7)
					MsSeek(xFilial("SE5")+cSeekSE5)
					While ( !Eof() .And. xFilial("SE5")	== NEWSE5->E5_FILIAL .And.;
							cSeekSE5			== NEWSE5->E5_PREFIXO+;
							NEWSE5->E5_NUMERO+;
							NEWSE5->E5_PARCELA+;
							NEWSE5->E5_TIPO+;
							NEWSE5->E5_CLIFOR+;
							NEWSE5->E5_LOJA+;
							NEWSE5->E5_SEQ )
						If ( NEWSE5->E5_TIPODOC != "ES" )
							aadd(aBaixas,{ NEWSE5->E5_MOTBX,NEWSE5->E5_SEQ,NEWSE5->(Recno()) })
						EndIf
						dbSelectArea("NEWSE5")
						dbSkip()
					EndDo
					dbSelectArea(cAliasSE5)
					u_fe440DeleB(aBaixas,If(MV_PAR05==1,.T.,.F.),;
						If(MV_PAR06==1,.T.,.F.);
						,"FINE440",mv_par03,mv_par04)
				Next nCntFor
			EndIf
			dbSelectArea(cAliasSE5)
			aBaixas := {}
			aBxEst  := {}
			cQuebra :=  (cAliasSE5)->E5_PREFIXO+(cAliasSE5)->E5_NUMERO+(cAliasSE5)->E5_PARCELA+;
				(cAliasSE5)->E5_TIPO+(cAliasSE5)->E5_CLIFOR+(cAliasSE5)->E5_LOJA
		EndIf
	EndDo
	If lQuery
		dbSelectArea(cAliasSE5)
		dbCloseArea()
		dbSelectArea("SE5")
	Else
		dbSelectArea("SE5")
		RetIndex("SE5")
		dbClearFilter()
		FErase(cArqInd+OrdBagExt())
	EndIf
Else
	Help(" ",1,"FA440FALHA")
EndIf
dbSelectArea("NEWSE5")
dbCloseArea()
dbSelectArea("SE5")
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FE440CalcB³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua c lculo das comissoes pela baixa                    ³±±
±±³          ³ Deve ser chamada no ponto de entrada da baixa do titulo da ³±±
±±³          ³ Liquidacao, pois o mesmo nao calcula comissao normalmente  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aBaixas   : array c/ dados da baixa                        ³±±
±±³          ³ lJuros    : Considera Juros                                ³±±
±±³          ³ lDescont  : Considera Desconto                             ³±±
±±³          ³ lDevolucao: Considera Devolucao                            ³±±
±±³          ³ cOrigem   : Origem da Comissao                             ³±±
±±³          ³ cSinal    : Sinal da Comissao (+/-)                        ³±±
±±³	     ³ cVendDe   : Vendedor Inicial        			  ³±±
±±³	     ³ cVendAte  : Vendedor Final           			  ³±±
±±³	     ³ lGoto     : Se Deve ser posicionado o SE1 (Opcional)       ³±±
±±³	     ³ nSE1Rec   : Registro do SE1 a ser posicionado    	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINE440 e Ponto de Entrada na Baixa do Titulo              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function fe440CalcB(aBaixas,lJuros,lDescont,cOrigem,cSinal,cVendDe,cVendAte,lGoto,nSE1Rec)

Local aArea 	:= GetArea()
Local aAreaSA3  := SA3->(GetArea())
Local aAreaSE1  := SE1->(GetArea())
Local aAreaSE5  := SE5->(GetArea())
Local aAreaAux  := {}
Local aStruSE5  := {}
Local nCntFor	:= 0
Local nCntFor2  := 0
Local nDescont  := 0
Local nVlrRec	:= 0
Local nJuros	:= 0
Local nCorrec	:= 0
Local nMulta	:= 0
Local nVlrLiq	:= 0
Local nVlrProp  := 0
Local nValor	:= 0
Local nQtdVend  := 0
Local nRegSE1   := 0
Local nVlrFatura:= 0
Local cQuebra   := ""
Local cVendedor := ""
Local cSeq 	:= ""
Local cVend     := "1"
Local cFatura   := ""
Local cNumLiq   := ""
Local cQuery    := ""
Local cAliasSE5 := "SE5"
Local dData		:= Ctod("")
Local lContinua := .F.
Local lFatura   := .F.
Local lLiquid   := .F. 
Local lDevolucao:= GetMv("MV_COMIDEV")
Local lCompensa := If(GetMv("MV_COMISCR")== "S",.T.,.F.)
Local lPago     := .F.
Local lQuery    := .F.
Local aBases    := {}
Local nDecimal  := TamSX3("E1_BASCOM1")[2]
Local nBaseComis:= 0
Local nBaseEmis := 0
Local nBaseBaix := 0
Local nVlrEmis  := 0
Local nVlrBaix  := 0
Local nDia      := 0
Local nMes      := 0
Local nAno      := 0
Local nPerComis := 0
Local nX        := 0
Local nIrrf     := 0
Local dVencto
Local lPerComis := .F.
LOCAL lCalcComis := .F.
Local lProcess := .T.
Local nTamParc := TamSx3("E1_PARCELA")[1]
Local cPrimParc := " "

cOrigem   := If( cOrigem == Nil ,"FINE440",cOrigem)
cSinal    := If( cSinal  == Nil ,"+",cSinal)
cVendDe   := If( cVendDe==Nil ,Space(Len(SE3->E3_VEND)),cVendDe)
cVendAte  := If( cVendAte==Nil,Repl("z",Len(SE3->E3_VEND)),cVendAte)
lGoto		 := If( lGoto == Nil ,.F.,lGoto)

// Verifica se é a primeira parcela de uma fatura
If nTamParc == 1
	cPrimParc := "1A "
ElseIf nTamparc == 2
	cPrimParc := GetMv("MV_1DUP")+Space(2-Len(GetMv("MV_1DUP")))
	cPrimParc += "#1 #A #  #01"
Else	
	cPrimParc := GetMv("MV_1DUP")+Space(3-Len(GetMv("MV_1DUP")))
	cPrimParc += "#1  #A  #   #001"
Endif

If (Len(aBaixas) > 0 )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posicionando Registros.                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE5")
	MsGoto(aBaixas[1][3])

	dbSelectArea("SE1")
	dbSetOrder(2)
	If ( !lGoto )
		MsSeek(xFilial("SE1")+SE5->E5_CLIFOR+SE5->E5_LOJA+SE5->E5_PREFIXO+;
			SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO)
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se devo mover para o registro fisico. Isto se d  em ³
		//³ virtude de no SQL o registro sair do filtro quando deixar de ³
		//³ atender a alguma das condicoes do mesmo.                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MsGoto(nSE1Rec)
	EndIf

	If ExistBlock("F440LIQ")
		lProcess := ExecBlock("F440LIQ",.F.,.F.)
	EndIf
	
	If (( SE1->(Found()) .Or. lGoto ) .and. lProcess .And. Empty(SE1->E1_TIPOLIQ) )
		lFatura	 := If(SE1->E1_FATURA=="NOTFAT",.T.,.F.)
		lLiquid  := !Empty(SE1->E1_NUMLIQ)
		If ( !lFatura .And. !lLiquid )
			If !(SE1->E1_TIPO$MVPAGANT+"/"+MVTAXA+"/"+MVPROVIS+"/"+MVRECANT+"/"+MVABATIM+"/"+MV_CPNEG+"/"+MV_CRNEG) .or. ;
					(cPaisLoc<>"BRA" .And. SE1->E1_TIPO $ MV_CRNEG)
				aBases   := Fe440Comis(SE1->(Recno()),cOrigem$"FINE440#FINA087A",cOrigem$"FINE440#FINA087A")
				nQtdVend := Len(aBases)
				cVend := "1"
			EndIf
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aqui e' verificado os vendedores para os titulo aglutinados  ³
			//³ na fatura a receber.                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			nRegSE1 := SE1->(Recno())
			If lFatura
				cFatura := SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_PREFIXO+SE1->E1_NUM
				dbSelectArea("SE1")
				dbSetOrder(10)
				MsSeek(xFilial("SE1")+cFatura,.F.)
				lPerComis := .F.
				nPerComis := 0
				While ( !Eof() .And. xFilial("SE1") == SE1->E1_FILIAL .And.;
						SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_FATPREF+SE1->E1_FATURA==;
						cFatura )
					dbSelectArea("SE1")
					aFatura := Fe440Comis(SE1->(Recno()),cOrigem$"FINE440",cOrigem$"FINE440")
					nQtdVend := Len(aFatura)
					cVend := "1"
					For nCntFor := 1 To Len(aFatura)
						aadd(abases,{	aFatura[nCntFor,1],;
							aFatura[nCntFor,2],;
							aFatura[nCntFor,3],;
							aFatura[nCntFor,4],;									
							aFatura[nCntFor,5],;
							aFatura[nCntFor,6],;
							aFatura[nCntFor,7]})
						If !lPerComis
							If ( nPerComis == 0 .And. nCntFor == 1 )
								nPerComis := aFatura[nCntFor][7]
							EndIf
							If ( nPerComis != aFatura[nCntFor][7] )
								lPerComis := .T.
								aFatura[nCntFor,7] := 0
							EndIf
						Else
							aFatura[nCntFor,7] := 0
						EndIf
					Next nCntFor
					If !SE1->E1_TIPO $ MVABATIM
						nVlrFatura += SE1->E1_VLCRUZ
					Endif
					dbSelectArea("SE1")
					dbSkip()
				EndDo
			Else
				cNumLiq := SE1->E1_NUMLIQ
				aAreaAux := SE5->(GetArea())
				dbSelectArea("SE5")
				dbSetOrder(10)
				#IFDEF TOP
					lQuery := .T.
					aStruSE5 := SE5->(dbStruct())
					cAliasSE5 := "FE440CALCB"
					cQuery := "SELECT * "
					cQuery += "FROM "+RetSqlName("SE5")+" SE5 "
					cQuery += "WHERE SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
					cQuery += "SE5.E5_DOCUMEN='"+cNumLiq+"' AND "
					cQuery += "SE5.E5_MOTBX = 'LIQ' AND "
					cQuery += "SE5.D_E_L_E_T_=' ' "
					
					cQuery := ChangeQuery(cQuery)
					
					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE5)
					
					For nX := 1 To Len(aStruSE5)
						If aStruSE5[nX][2]<>"C"
							TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
						EndIf
					Next nX
				#ELSE
					MsSeek(xFilial("SE5")+cNumLiq,.F.)
				#ENDIF
				lPerComis := .F.
				nPerComis := 0
				While ( !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
						SubStr((cAliasSE5)->E5_DOCUMEN,1,Len(cNumLiq)) == cNumLiq )
					If (cAliasSE5)->E5_MOTBX == "LIQ"
						dbSelectArea("SE1")
						dbSetOrder(2)
						If MsSeek(xFilial("SE1")+(cAliasSE5)->E5_CLIFOR+(cAliasSE5)->E5_LOJA+(cAliasSE5)->E5_PREFIXO+(cAliasSE5)->E5_NUMERO+(cAliasSE5)->E5_PARCELA+(cAliasSE5)->E5_TIPO)
            	
							aFatura := Fe440Comis(SE1->(Recno()),cOrigem$"FINE440",cOrigem$"FINE440")
							nQtdVend := Len(aFatura)
							cVend := "1"
							For nCntFor := 1 To Len(aFatura)
								aadd(abases,{aFatura[nCntFor,1],;
									aFatura[nCntFor,2],;
									aFatura[nCntFor,3],;
									aFatura[nCntFor,4],;									
									aFatura[nCntFor,5],;
									aFatura[nCntFor,6],;
									aFatura[nCntFor,7]})
								If !lPerComis
									If ( nPerComis == 0 .And. nCntFor == 1 )
										nPerComis := aFatura[nCntFor][7]
									EndIf
									If ( nPerComis != aFatura[nCntFor][7] )
										lPerComis := .T.
										aFatura[nCntFor,7] := 0
									EndIf
								Else
									aFatura[nCntFor,7] := 0
								EndIf
							Next nCntFor
							If !SE1->E1_TIPO $ MVABATIM
								nVlrFatura += SE1->E1_VLCRUZ
							EndIf
						EndIf
					EndIf
					dbSelectArea(cAliasSE5)
					dbSkip()
				EndDo
				If lQuery
					dbSelectArea(cAliasSE5)
					dbCloseArea()
					dbSelectArea("SE5")
				EndIf				
			EndIf
			RestArea(aAreaAux)
			dbSelectArea("SE1")
			dbSetOrder(1)
			MsGoto(nRegSE1)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aqui e' feito o ajuste das bases em relacao ao titulo.       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			dbSelectArea("SE1")
			nVlrProp := ((SE1->E1_VLCRUZ*10000)/nVlrFatura)/10000
			lPerComis := .F.
			nPerComis := 0
			aBases := aSort(aBases,,,{|x,y| x[1] < y[1] })
			cVendedor := ""
			For nCntFor := 1 To Len(aBases)
				nBaseComis:= NoRound(aBases[nCntFor,2]*nVlrProp,nDecimal)
				nBaseEmis := NoRound(aBases[nCntFor,3]*nVlrProp,nDecimal)
				nBaseBaix := NoRound(aBases[nCntFor,4]*nVlrProp,nDecimal)
				nVlrEmis  := NoRound(aBases[nCntFor,5]*nVlrProp,nDecimal)
				nVlrBaix  := NoRound(aBases[nCntFor,6]*nVlrProp,nDecimal)
				If ( cVendedor <> aBases[nCntFor][1] )
					//--> Base da Comissao
					nBaseDif := NoRound(aBases[nCntFor,2]*(1-nVlrProp),nDecimal)
					nBaseDif := aBases[nCntFor,2]-nBaseDif-nBaseComis
					aBases[nCntFor,2] := nBaseComis+nBaseDif

					//--> Base da Comissao na Emissao
					nBaseDif := NoRound(aBases[nCntFor,3]*(1-nVlrProp),nDecimal)
					nBaseDif := aBases[nCntFor,3]-nBaseDif-nBaseEmis
					aBases[nCntFor,3] := nBaseEmis+nBaseDif

					//--> Base da Comissao na Baixa
					nBaseDif := NoRound(aBases[nCntFor,4]*(1-nVlrProp),nDecimal)
					nBaseDif := aBases[nCntFor,4]-nBaseDif-nBaseBaix
					aBases[nCntFor,4] := nBaseBaix+nBaseDif

					//--> Valor da Comissao na Emissao
					nBaseDif := NoRound(aBases[nCntFor,5]*(1-nVlrProp),nDecimal)
					nBaseDif := aBases[nCntFor,5]-nBaseDif-nVlrEmis
					aBases[nCntFor,5] := nVlrEmis+nBaseDif

					//--> Valor da Comissao na Baixa
					nBaseDif := NoRound(aBases[nCntFor,6]*(1-nVlrProp),nDecimal)
					nBaseDif := aBases[nCntFor,6]-nBaseDif-nVlrBaix
					aBases[nCntFor,6] := nVlrBaix+nBaseDif
				Else
					aBases[nCntFor,2] := nBaseComis
					aBases[nCntFor,3] := nBaseEmis
					aBases[nCntFor,4] := nBaseBaix
					aBases[nCntFor,5] := nVlrEmis
					aBases[nCntFor,6] := nVlrBaix
				EndIf
				If !lPerComis
					If ( nPerComis == 0 .And. nCntFor == 1 )
						nPerComis := aBases[nCntFor][7]
					EndIf
					If ( nPerComis != aBases[nCntFor][7] )
						lPerComis := .T.
						aBases[nCntFor,7] := 0
					EndIf
				Else
					aBases[nCntFor,7] := 0
				EndIf
				cVendedor := aBases[nCntFor][1]
			Next nCntFor
		EndIf
		nDescont  := 0
		nVlrRec   := 0
		nJuros    := 0
		nCorrec   := 0
		nMulta    := 0
		cQuebra   := ""
		cSeq		 := ""
		aadd(aBaixas,{"zzz","zz",0}) // Controle de Saida
		For nCntFor := 1 To Len(aBaixas)
			If ( If(!lDevolucao,aBaixas[nCntFor,1]!="DEV",.T.) .And. ;
					If(!lCompensa ,aBaixas[nCntFor,1]!="CMP",.T.))
				dbSelectArea("SE5")
				If ( aBaixas[nCntFor,3] != 0 )
					MsGoto(aBaixas[nCntFor,3])
				Else
					cQuebra := ""	
				EndIf
				lCalcComis := ComisBx(SE5->E5_MOTBX)
				If ( cQuebra != aBaixas[nCntFor,1]+aBaixas[nCntFor,2] )
					If ( !Empty(cQuebra) .Or. aBaixas[nCntFor,3] == 0 )
						For nCntFor2 := 1 To Len(aBases)
							If ( aBases[nCntFor2,1] >= cVendDe  .And.;
									aBases[nCntFor2,1] <= cVendAte )

								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Calcula o Valor Recebido sobre o Principal       ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								nVlrLiq :=  nVlrRec - If(lJuros,0,nJuros)
								nVlrLiq +=  If(lDescont,0,nDescont)
								nVlrLiq -=  nCorrec
								nVlrLiq -=  nMulta
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Calcula o Valor Proporcional de IR na Base da    ³
								//³ Comissao, quando considerar IR na base de calculo³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If cPaisLoc == "BRA"
									If GetMv("MV_COMISIR") == "S"
										nIrrf := (SE1->E1_IRRF*nVlrLiq)/(SE1->E1_VLCRUZ-SE1->E1_IRRF)
									Else
										nIrrf := 0
									EndIf
									nVlrliq  += nIrrf
								Endif
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Calcula o Valor Proporcional - Base da Comissao  ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If cPaisLoc == "BRA" .Or. (cPaisLoc<>"BRA" .And. aBases[nCntFor2,2] == 0)
									nVlrProp := NoRound((aBases[nCntFor2,4]*nVlrLiq)/SE1->E1_VLCRUZ,nDecimal+1)
									nValor   := NoRound((aBases[nCntFor2,6]*nVlrLiq)/SE1->E1_VLCRUZ,nDecimal+1)
								Else
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Localizacoes: SE1->E1_VLCRUZ considera frete, lo-³
									//³ go tiro diferenca com relacao a base calculada   ³
									//³ antes de calcular o valor proporcional.          ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									nVlrLiq	:= (aBases[nCntFor2,2]*nVlrLiq)/SE1->E1_VLCRUZ
									nVlrProp := (aBases[nCntFor2,4]*nVlrLiq)/aBases[nCntFor2,2]
									nValor   := (aBases[nCntFor2,6]*nVlrLiq)/aBases[nCntFor2,2]
								EndIf

								If ( aBases[nCntFor2,6] != 0 .And. nVlrProp > 0 )
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Grava resultado no arquivo de pgto. de comissäes.    ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									SA3->(MsSeek(xFilial("SA3")+aBases[nCntFor2,1]))
									dbSelectArea("SE3")
									dbSetOrder(3)
									MsSeek(xFilial("SE3")+aBases[nCntFor2,1]+;
										SE1->E1_CLIENTE+SE1->E1_LOJA+;
										SE1->E1_PREFIXO+;
										SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+cSeq)
									If ( Found() )
										If ( !Empty(SE3->E3_DATA) )
											lPago := .T.
											If (! cOrigem $ "FINE440" ) //.And. cSinal == "-" )
												RecLock("SE3",.T.)
											EndIf
										Else
											lPago := .F.
											RecLock("SE3",.F.)
										EndIf
									Else
										lPago := .F.
										RecLock("SE3",.T.)
									EndIf
									If ((!lPago .Or. !(cOrigem $ "FINE440")) .And. nValor!=0)
										SE3->E3_BASE    += (nVlrProp * Iif( cSinal == "-" .Or.( SE3->E3_TIPO $ MV_CRNEG  .And. cPaisLoc <>"BRA"),-1,1))
										SE3->E3_COMIS   += (nValor* Iif( cSinal == "-" .Or.( SE3->E3_TIPO $ MV_CRNEG  .And. cPaisLoc <>"BRA"),-1,1))
										SE3->E3_FILIAL  := xFilial()
										SE3->E3_VEND    := aBases[nCntFor2,1]
										SE3->E3_SERIE   := SE1->E1_SERIE
										SE3->E3_NUM     := SE1->E1_NUM
										SE3->E3_CODCLI  := SE1->E1_CLIENTE
										SE3->E3_LOJA    := SE1->E1_LOJA
										SE3->E3_EMISSAO := dData
										SE3->E3_PREFIXO := SE1->E1_PREFIXO
										SE3->E3_PARCELA := SE1->E1_PARCELA
										SE3->E3_TIPO    := SE1->E1_TIPO
										SE3->E3_BAIEMI  := "B"
										SE3->E3_ORIGEM  := Fe440Origem(cOrigem)
										SE3->E3_PEDIDO  := SE1->E1_PEDIDO
										SE3->E3_SEQ     := cSeq
										SE3->E3_PORC    := Abs(SE3->E3_COMIS/SE3->E3_BASE*100)
										If ( aBases[nCntFor2,7] != 0 )
											SE3->E3_PORC    := aBases[nCntFor2,7]
										EndIf
										If Empty( SA3->A3_DIA )
											dVencto := dData
										Else
											dVencto := Ctod( strzero(SA3->A3_DIA,2)+"/"+;
												strzero(Month(dData),2)+"/"+;
												strzero( Year(dData),4),"ddmmyy")
											nDia := SA3->A3_DIA
											While empty( dVencto)
												nDia -= 1
												dVencto := CtoD(strzero(nDia,2)+"/"+;
													strzero(month(dData),2)+"/"+;
													strzero( year(dData),4),"ddmmyy")
											endDo
										EndIf

										if SA3->A3_DDD == "F" .or. dVencto < dData		//Fora o mes
											nDia := SA3->A3_DIA
											nMes := month(dVencto) + 1
											nAno := year (dVencto)
											If nMes == 13
												nMes := 01
												nAno := nAno + 1
											Endif
											nDia	  := strzero(nDia,2)
											nMes	  := strzero(nMes,2)
											nAno	  := substr(lTrim(str(nAno)),3,2)
											dVencto := CtoD(nDia+"/"+nMes+"/"+nAno,"ddmmyy")
										Else
											nDia	  := strzero(day(dVencto),2)
											nMes	  := strzero(month(dVencto),2)
											nAno	  := substr(lTrim(str(Year(dVencto))),3,2)
										Endif

										While empty( dVencto)
											nDia := if(Valtype(nDia)=="C",Val(nDia),nDia)
											nDia -= 1
											dVencto := CtoD(strzero(nDia,2)+"/"+nMes+"/"+nAno,"ddmmyy")
											if !empty( dVencto )
												if dVencto < dData
													dVencto += 2
												EndIf
											EndIf
										EndDo

										SE3->E3_VENCTO  := dVencto

										If ExistBlock("MSE3440")
											ExecBlock("MSE3440",.F.,.F.,{nDescont,nJuros,cOrigem})
										EndIf
										MsUnlock()
									EndIf
								EndIf
							EndIf
						Next nCntFor2
						nDescont  := 0
						nVlrRec   := 0
						nJuros    := 0
						nCorrec   := 0
						nMulta    := 0
						If aBaixas[nCntFor,3] == 0
							Exit
						EndIf
					EndIf
					cQuebra   := aBaixas[nCntFor,1]+aBaixas[nCntFor,2]
					cSeq		 := SE5->E5_SEQ
					dData     := SE5->E5_DATA
				EndIf
				If ( SE5->E5_TIPODOC $ "VL#BA#V2#CP#LJ" )  .and. lCalcComis
					nVlrRec  += SE5->E5_VALOR
				Endif
				If ( SE5->E5_TIPODOC$"CM#C2#CX" ) .and. lCalcComis
					nCorrec += SE5->E5_VALOR
				Endif
				If ( SE5->E5_TIPODOC$"DC#D2" ) .and. lCalcComis
					nDescont += SE5->E5_VALOR
				Endif
				If ( SE5->E5_TIPODOC$"MT#M2" ) .and. lCalcComis
					nMulta  += SE5->E5_VALOR
				Endif
				If ( SE5->E5_TIPODOC$"JR#J2" ) .and. lCalcComis
					nJuros  += SE5->E5_VALOR
				EndIf
			EndIf
		Next nCntFor
	EndIf
EndIf
RestArea(aAreaSE5)
RestArea(aAreaSE1)
RestArea(aAreaSA3)
RestArea(aArea)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fe440ChecF³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua sele‡„o dos T¡tulos.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINE440                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fe440Checf(nFiltro,lQuery)

Local cFiltro       := ""
Local cQuery        := ""
Local lComiDevol	:= GetMv("MV_COMIDEV")
Local lCalcComis	:= .T.

lQuery := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Caso motivo da baixa seja DEVOLUCAO e lCalcComis = .F.	³
//³ NO calcula a comiss„o . O Valor de lCalcComis vem do 	³
//³ parƒmetro MV_COMIDEV									³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ( nFiltro == 1 )
	cFiltro := 'E1_FILIAL=="'+xFilial("SE1")+'".And.'
	cFiltro += 'DTOS(E1_EMISSAO)>="'+DTOS(mv_par01)+'".And.'
	cFiltro += 'DTOS(E1_EMISSAO)<="'+DTOS(mv_par02)+'".And.'
	cFiltro += '!(E1_TIPO $ "' + MVRECANT + If(!lComiDevol,'#'+MV_CRNEG,'')+'")'
EndIf
If ( nFiltro == 2 )
	cFiltro := 'E5_FILIAL=="'+xFilial("SE5")+'".And. '
	cQuery  := "E5_FILIAL='"+xFilial("SE5")+"' AND "
	cFiltro += "Dtos(E5_DATA)>='" + Dtos(mv_par01) + "'.And. "
	cQuery  += "E5_DATA >= '"+dtos(mv_par01) + "' AND "
	cFiltro += "Dtos(E5_DATA)<='" + Dtos(mv_par02) + "'.And. "
	cQuery  += "E5_DATA <= '"+Dtos(mv_par02) + "' AND "
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Caso parƒmetro MV_COMIDEV = .F.	, desconsidera o baixa de³
	//³ titulo por devolucao para fins de recalculo de comissao. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lComiDevol
		cFiltro += 'E5_MOTBX!="DEV".And. '
		cQuery  += "E5_MOTBX<>'DEV' AND "
	Endif
	cFiltro += "E5_MOTBX == 'LIQ' .And. "
	cQuery  += "E5_MOTBX = 'LIQ' AND "
	cFiltro += '(E5_RECPAG=="R" .OR. (E5_RECPAG=="P".And.E5_TIPODOC=="ES")) '
	cQuery  += "(E5_RECPAG='R' OR (E5_RECPAG='P' AND E5_TIPODOC='ES')) "
EndIf
If ( nFiltro == 3 )
	cFiltro := 'E3_FILIAL=="'+xFilial("SE3")+'".And.'
	cFiltro += 'Dtos(E3_EMISSAO)>="'+Dtos(mv_par01)+'".And.'
	cFiltro += 'Dtos(E3_EMISSAO)<="'+Dtos(mv_par02)+'".And.'
	cFiltro += 'E3_VEND>="'+mv_par03+'".And.'
	cFiltro += 'E3_VEND<="'+mv_par04+'".And.'
	cFiltro += 'Dtos(E3_DATA)=="'+Dtos(cTod("//"))+'".And.'
	cFiltro += 'E3_ORIGEM!=" "'
EndIf

Return IIF(lQuery,cFiltro,cQuery)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fe440Comis³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua o calculo das bases da comissao de um determinado   ³±±
±±³          ³ t¡tulo financeiro.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Numero do Registro do SE1 ( Contas a Receber )     ³±±
±±³          ³ ExpL2 : .T. : Atualiza baseS da comissao do SE1.		  ³±±
±±³          ³ ExpL3 : .F. : Retorna bases do SE1 ou (.T.) recalcula(Defa)³±±
±±³          ³ ExpN4 : Numero do Registro do SD2 para Devol.de Vendas     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Retorna um Array com as bases da comissao. na seguinte     ³±±
±±³          ³ estrutura [C¢digo do Vendedor]                             ³±±
±±³          ³           [Base da Comissao]                               ³±±
±±³          ³           [Base na Emissao ]                               ³±±
±±³          ³           [Base na Baixa   ]                               ³±±
±±³          ³           [Vlr  na Emissao ]                               ³±±
±±³          ³           [Vlr  na Baixa   ]                               ³±±
±±³	     ³ 		 [% da Comissao/Se "Zero" diversos %'s]           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Fina440                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fe440Comis(nRegistro,lGrava,lRefaz,nRegDevol)

Local aArea 	 := GetArea()
Local aAreaSE4  := SE4->(GetArea())
Local aAreaSF2  := SF2->(GetArea())
Local aAreaSF4  := SF4->(GetArea())
Local aAreaSD2  := SD2->(GetArea())
Local aAreaSA3  := SA3->(GetArea())
Local aAreaSD1  := SD1->(GetArea())
Local aAreaSF1  := SF1->(GetArea())
Local aAreaSE1  := SE1->(GetArea())
Local aAux      := {}
Local aSD2Vend  := {} // Array c/ as Bases dos Vend. por item de nota
Local aBaseNCC  := {} // Array c/ as Bases dos Vend.
Local aBaseSE1  := {} // Array c/ as Bases dos Vend. do Titulo em questao
Local aBaseSD1  := {} // Array c/ as Bases dos Vend. do Item da NFE
Local nVend     := U_fe440CntVen() // Numero M ximo de Vendedores
Local nCntFor   := 0
Local nMaxFor   := 0
Local nBaseSe1  := 0 // Base da Comissao
Local nBaseDif  := 0
Local nPerComis := 0 // Percentual da Comissao
Local nBaseEmis := 0 // Base da Comissao na Emissao
Local nBaseBaix := 0 // Base da Comissao na Baixa
Local nVlrEmis  := 0 // Valor da Comissao na Emissao
Local nVlrBaix  := 0 // Valor da Comissao na Baixa
Local nFrete    := 0 // Valor do Frete
Local nIcmFrete := 0 // Valor do Icms sobre frete
Local nTotal    := 0 // Total das mercadorias pelo item
Local nRatFrete := 0 // Valor rateado do Frete por item
Local nRatIcmFre:= 0 // Valor rateado do icms s/ frete por item
Local nRatIpiFre:= 0 // Valor rateado do ipi s/ frete por item
Local nSF2IcmRet:= 0 // Valor do Icms Retido
Local nVlrFat   := 0 // Valor faturado
Local nVlrTit   := 0 // valor do titulo em questao
Local nProp     := 0
Local nPos      := 0
Local cVend     := "1"
Local cVendedor := ""
Local cSerie    := ""
Local cPrefixo  := "" // Prefixo da Duplicata
Local lContinua := .T.
Local nDecimal  := TamSX3("E1_BASCOM1")[2] // N@ de decimais considerados no calculo
Local lPerComis := .F.
Local cFilialSF2:= xFilial("SF2")
Local cFilialSE4:= xFilial("SE4")
Local cFilialSD2:= xFilial("SD2")
Local cFilialSF4:= xFilial("SF4")
Local cFilialSF1:= xFilial("SF1")
Local cFilialSD1:= xFilial("SD1")
Local lQuery    := .F.
Local cQuery    := ""
Local cAliasSD1 := "SD1"
Local cAliasSD2 := "SD2"
Local cAliasDev := "SD1"
Local cAliasSF4 := "SF4"
Local cImp		:= "N"	 // Se A3_COMIMP nao existe, cImp = N, senao pega valor em A3_COMIMP
Local aImp		:= {}  // Recebe o array de TesImpInf
Local lPEPerCom := ExistBlock("FIN440PE")
Local aSemNota	:=	{}
Local cEspSes	:= " "
Local nTamParc  := TamSx3("E1_PARCELA")[1]
Local cPrimParc := " "
Local nAlEmissao:= 0
Local nAlBaixa  := 0

Static aStruSD1
Static aStruSD2
Static aStruSF4

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se é a primeira parcela de uma fatura               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTamParc == 1
	cPrimParc := "1A "
ElseIf nTamparc == 2
	cPrimParc := GetMv("MV_1DUP")+Space(2-Len(GetMv("MV_1DUP")))
	cPrimParc += "#1 #A #  #01"
Else	
	cPrimParc := GetMv("MV_1DUP")+Space(3-Len(GetMv("MV_1DUP")))
	cPrimParc += "#1  #A  #   #001"
Endif

if cPaisLoc<>"BRA"
	cEspSes	:= GetSesNew("NDC","1")
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento de Parametros Default da funcao                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lRefaz := If( lRefaz == Nil , .T. , lRefaz )
lGrava := If( lGrava == Nil , .T. , lGrava )
If ( aStruSD1 == Nil )
	aStruSD1 := {}
	dbSelectArea("SD1")
	aAux := dbStruct()
	For nCntFor := 1 To Len(aAux)
		If ( FieldName(nCntFor)$"D1_FILIAL#D1_DOC#D1_SERIE#D1_FORNECE#D1_LOJA" .Or.;
				FieldName(nCntFor)$"D1_NFORI#D1_SERIORI#D1_COD#D1_ITEMORI#D1_TOTAL#D1_VALDESC#D1_ITEM" )
			aadd(aStruSD1,{aAux[nCntFor,1],aAux[nCntFor,2],aAux[nCntFor,3],aAux[nCntFor,4]})
		EndIf
	Next nCntFor
EndIf
If ( aStruSD2 == Nil )
	aStruSD2 := {}
	dbSelectArea("SD2")
	aAux := dbStruct()
	For nCntFor := 1 To Len(aAux)
		If ( FieldName(nCntFor)$"D2_FILIAL#D2_DOC#D2_SERIE#D2_CLIENTE#D2_LOJA#D2_TES#D2_ICMFRET" .Or.;
				FieldName(nCntFor)$"D2_TOTAL#D2_VALICM#D2_VALIPI#D2_IPI#D2_ICMSRET#D2_VALACRS#D2_ITEM#D2_COD" .Or.;
				"D2_COMIS"$FieldName(nCntFor) )
			aadd(aStruSD2,{aAux[nCntFor,1],aAux[nCntFor,2],aAux[nCntFor,3],aAux[nCntFor,4]})
		EndIf
	Next nCntFor
EndIf
If ( aStruSF4 == Nil )
	aStruSF4 := {}
	dbSelectArea("SF4")
	aAux := dbStruct()
	For nCntFor := 1 To Len(aAux)
		If ( FieldName(nCntFor)$"F4_DUPLIC#F4_INCIDE#F4_IPIFRET#F4_INCSOL#F4_ISS" )
			aadd(aStruSF4,{aAux[nCntFor,1],aAux[nCntFor,2],aAux[nCntFor,3],aAux[nCntFor,4]})	
		EndIf
	Next nCntFor
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona no Registro do SE1 a ser calculado                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SE1")
MsGoto(nRegistro)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o titulo foi gerado pelo faturamento e for neces-³
//³ sario recalcular suas bases.                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SE1->E1_TIPO $ MVNOTAFIS+cEspSes )
	For nCntFor := 1 To nVend 	
		cVendedor := SE1->(FieldGet(SE1->(FieldPos("E1_VEND"+cVend))))
		nPerComis := SE1->(FieldGet(SE1->(FieldPos("E1_COMIS"+cVend))))
		If ( nPerComis == 0 .And. !Empty(cVendedor) )
			lRefaz := .T.
			Exit
		EndIf
		cVend := Soma1(cVend,1)
	Next nCntFor
ElseIf	cPaisLoc <>"BRA" .and.( SE1->E1_TIPO $ MV_CRNEG+MVRECANT)
	lRefaz := .T. 
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona Registros                                          ³
//³ Aqui se faz necessaria a cria‡Æo de tratamento de filial de  ³
//³ origem para quando se tem SE1 compartilhado e SF2, SE4 ou SD2³
//³ exclusivos                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SE1->(FieldPos("E1_FILORIG")) > 0
	If !Empty(SE1->E1_FILORIG) .and. !(Empty(cFilialSf2))
		cFilialSf2 := SE1->E1_FILORIG
	Endif
	If !Empty(SE1->E1_FILORIG) .and. !(Empty(cFilialSe4))
		cFilialSe4 := SE1->E1_FILORIG
	Endif
	If !Empty(SE1->E1_FILORIG) .and. !(Empty(cFilialSd2))
		cFilialSd2 := SE1->E1_FILORIG
	Endif
	If !Empty(SE1->E1_FILORIG) .and. !(Empty(cFilialSf4))
		cFilialSf4 := SE1->E1_FILORIG
	Endif
Endif
If ( SE1->E1_TIPO $ MVNOTAFIS+cEspSes .And. lRefaz)
	If Year(SE1->E1_EMISSAO)<2001
		cSerie := If(Empty(SE1->E1_SERIE),SE1->E1_PREFIXO,SE1->E1_SERIE)
	Else
		cSerie := SE1->E1_SERIE
	EndIf
	dbSelectArea("SF2")
	dbSetOrder(1)
	If (!MsSeek(cFilialSF2+SE1->E1_NUM+cSerie+SE1->E1_CLIENTE+SE1->E1_LOJA,.F.))
		lContinua := .F.
	EndIf
	dbSelectArea("SE4")
	dbSetOrder(1)
	If (!MsSeek(cFilialSE4+SF2->F2_COND))
		lContinua := .F.
	EndIf
	dbSelectArea("SD2")
	dbSetOrder(3)
	#IFDEF TOP
		If ( TcSrvType()!="AS/400" )
			SD2->(dbCommit())
			lQuery := .T.
			cAliasSD2 := "FA440COMIS"
			cAliasSF4 := "FA440COMIS"
			cQuery := ""
			For nCntFor := 1 To Len(aStruSD2)
				cQuery += ","+aStruSD2[nCntFor][1]
			Next nCntFor
			For nCntFor := 1 To Len(aStruSF4)
				cQuery += ","+aStruSF4[nCntFor][1]
			Next nCntFor
			cQuery := "SELECT SD2.R_E_C_N_O_ SD2RECNO,"+SubStr(cQuery,2)
			cQuery += "FROM "+RetSqlName("SD2")+" SD2,"+RetSqlName("SF4")+" SF4 "
			cQuery += "WHERE SD2.D2_FILIAL='"+cFilialSD2+"' AND "
			cQuery += "SD2.D2_DOC='"+SE1->E1_NUM+"' AND "
			cQuery += "SD2.D2_SERIE='"+cSerie+"' AND "
			cQuery += "SD2.D2_CLIENTE='"+SE1->E1_CLIENTE+"' AND "
			cQuery += "SD2.D2_LOJA='"+SE1->E1_LOJA+"' AND "
			cQuery += "SD2.D_E_L_E_T_<>'*' AND "
			cQuery += "SF4.F4_FILIAL='"+cFilialSF4+"' AND "
			cQuery += "SF4.F4_CODIGO=SD2.D2_TES AND "
			cQuery += "SF4.D_E_L_E_T_<>'*' "
			cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))
			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)
			lContinua := !(cAliasSD2)->(Eof())
			For nCntFor := 1 To Len(aStruSD2)
				If ( aStruSD2[nCntFor][2]!= "C" )
					TcSetField(cAliasSD2,aStruSD2[nCntFor][1],aStruSD2[nCntFor][2],aStruSD2[nCntFor][3],aStruSD2[nCntFor][4])
				EndIf
			Next nCntFor
			For nCntFor := 1 To Len(aStruSF4)
				If ( aStruSF4[nCntFor][2]!="C" )
					TcSetField(cAliasSF4,aStruSF4[nCntFor][1],aStruSF4[nCntFor][2],aStruSD2[nCntFor][3],aStruSF4[nCntFor][4])
				EndIf
			Next nCntFor
		Else
	#ENDIF	
		If (!MsSeek(cFilialSD2+SE1->E1_NUM+cSerie+SE1->E1_CLIENTE+SE1->E1_LOJA,.F.))
			lContinua := .F.
		EndIf
		#IFDEF TOP
		EndIf	
		#ENDIF
	If ( lContinua )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calculo da comissao por item de nota fiscal                  ³
		//³                                                              ³
		//³1)O Valor do icms s/ frete e adiciona ao campo F2_VALICM, por ³
		//³esta razao deve-se somar o vlr do icms dos itens e subtrair   ³
		//³do total de icms (F2_VALICM) para apurar-se o vlr icms s/frete³
		//³                                                              ³
		//³2)O mesmo ocorre para o valor do IPI sobre frete, Por esta ra-³
		//³zao e' calculado o valor do IPI sobre frete do item multipli- ³
		//³cando-se o valor do frete do item pelo % de ipi do item.      ³
		//³                                                              ³
		//³3)O Valor do Icms Retido pode nÆo estar no total da nota (F2_-³
		//³VALBRUT) por isto deve-se considerar o campo (D2_ICMSRET).    ³
		//³                                                              ³
		//³4)O percentual da comissao dever ser considerado para cada i- ³
		//³tem de nota fiscal pois ela pode ser diferente entre eles. O  ³
		//³percentual gravado no E1_COMIS ‚ sempre um valor aproximado e ³
		//³nao deve ser considerado ser houver nota fiscal para o titulo.³
		//³                                                              ³
		//³5)A Base da Comissao ‚ o valor da mercadoria + o valor do ipi ³
		//³+ o valor das despesas acessorias +  o icms solidario. Como e'³
		//³por item deve-se conhece-lo pelo item a item.                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotal     := 0
		nFrete     := (SF2->F2_FRETE + SF2->F2_SEGURO + SF2->F2_DESPESA)
		nIcmFrete  := 0
		nSF2IcmRet :=0
		While ( !Eof() .And. (cAliasSD2)->D2_FILIAL == cFilialSD2 .And.;
				(cAliasSD2)->D2_DOC 	 == SE1->E1_NUM .And.;
				(cAliasSD2)->D2_SERIE	 == cSerie .And.;
				(cAliasSD2)->D2_CLIENTE  == SE1->E1_CLIENTE .And.;
				(cAliasSD2)->D2_LOJA	 == SE1->E1_LOJA	)

			If ( !lQuery )
				dbSelectArea("SF4")
				dbSetOrder(1)
				MsSeek(cFilialSF4+(cAliasSD2)->D2_TES)
			EndIf

			cVend := "1"
			For nCntFor := 1 To nVend
				cVendedor := SF2->(FieldGet(SF2->(FieldPos("F2_VEND"+cVend))))
				nPerComis := (cAliasSD2)->(FieldGet((cAliasSD2)->(FieldPos("D2_COMIS"+cVend))))
				nImp := 0
				If cPaisLoc <> "BRA"
					SA3->(DbSetOrder(1))
					SA3->(DbSeek(xFilial()+cVendedor))
					aImp := TesImpInf(SD2->D2_TES)
					cImp := IIf(SA3->(FieldPos("A3_COMIMP")) >0,SA3->A3_COMIMP,"N")
					For nX :=1 to Len(aImp)
						If (cImp+aImp[nX][3] == "S1")
							nImp += SD2->(FieldGet(FieldPos(aImp[nX][2])))
						ElseIf (cImp+aImp[nX][3] == "N2")
							nImp -= SD2->(FieldGet(FieldPos(aImp[nX][2]))	)
						EndIf
					Next nX
				EndIf
				If ( !Empty(cVendedor) .And. (cAliasSF4)->F4_DUPLIC == "S" )
					aadd(aSD2Vend,{ cVendedor,;
						(cAliasSD2)->D2_TOTAL+nImp,;
						(cAliasSD2)->D2_VALICM,;
						(cAliasSD2)->D2_VALIPI,;
						(cAliasSD2)->D2_IPI,;
						(cAliasSF4)->F4_INCIDE,;
						(cAliasSF4)->F4_IPIFRET,;
						Iif((cAliasSF4)->F4_INCSOL=="N",0,(cAliasSD2)->D2_ICMSRET),;
						nPerComis,;
						(cAliasSD2)->D2_VALACRS,;
						If(lQuery,(cAliasSD2)->SD2RECNO,(cAliasSD2)->(RecNo())),;
						(cAliasSF4)->F4_ISS=="S",;
						cVend})
				EndIf							
				cVend := Soma1(cVend,1)
			Next nCntFor
			If ( (cAliasSF4)->F4_ISS != "S" )
				nSF2IcmRet += Iif((cAliasSF4)->F4_INCSOL=="N",0,(cAliasSD2)->D2_ICMSRET)
				If (cAliasSD2)->(FieldPos("D2_ICMFRET"))>0
					nIcmFrete  += (cAliasSD2)->D2_ICMFRET
				EndIf
			EndIf
			nTotal	  += (cAliasSD2)->D2_TOTAL
			dbSelectArea(cAliasSD2)
			dbSkip()
		EndDo
		If ( lQuery )
			dbSelectArea(cAliasSD2)
			dbCloseArea()
			dbSelectArea("SD2")
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calculo da comissao pela nota.                               ³
		//³                                                              ³
		//³1)Apos calculado as bases de cada vendedor por item de nota   ³
		//³deve-se aglutina-las formando uma unica base para toda a nota ³
		//³fiscal.                                                       ³
		//³                                                              ³
		//³2)Como os valores serao aglutinados pode-se haver perda de de-³
		//³cimais por isto deve-se haver um controle para que a perda se-³
		//³ja adicionada a primeira parcela da nota.                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nMaxFor := Len(aSD2Vend)
		nPerComis:=0
		lPerComis:=.F.
		For nCntFor := 1 To nMaxFor
			If (!lPePercom)
				If ( !lPerComis )
					If ( nPerComis == 0 .And. nCntFor == 1 )
						nPerComis := aSD2Vend[nCntFor][9]
					EndIf
					If ( nPerComis != aSD2Vend[nCntFor][9] )
						lPerComis := .T.
					EndIf
				EndIf
			Else
				nPerComis := ExecBlock("FIN440PE",.F.,.F.,{aSD2Vend[nCntFor][1]})
				If ( ValType(nPerComis)<>"N" )
					nPerComis := aSD2Vend[nCntFor][9]
					lPerComis := .F.
				Else
					lPerComis := .T.
				EndIf
			EndIf
			If ( SE1->E1_PARCELA $ cPrimParc )
				nBaseDif  := NoRound(nFrete*(1-(aSD2Vend[nCntFor,2]/nTotal)),nDecimal)
				nBaseDif  := nFrete - nBaseDif
				nRatFrete := nBaseDif
			Else
				nRatFrete := NoRound(nFrete*aSD2Vend[nCntFor,2]/nTotal,nDecimal)
			EndIf
			nRatIpiFre:= 0
			nRatIcmFre:= 0
			nBaseSE1  := 0
			nPos      := 0
			If ( aSD2Vend[nCntFor,7] $ "S " )
				nRatIpiFre:= NoRound(nRatFrete*aSD2Vend[nCntFor,5]/100,nDecimal)
			Else
				nRatIpiFre:= 0
			EndIf
			nRatIcmFre:= NoRound(nIcmFrete*aSD2Vend[nCntFor,2]/nTotal,nDecimal)
			nBaseSE1  := aSD2Vend[nCntFor,2]+aSD2Vend[nCntFor,4]+aSD2Vend[nCntFor,8]+nRatFrete+nRatIpiFre
			dbSelectArea("SA3")
			dbSetOrder(1)
			MsSeek(xFilial()+aSD2Vend[nCntFor,1])

			If SE1->(FieldPos("E1_ALEMIS"+aSD2Vend[nCntFor,13]))<>0 //Nao criar no dicionario padrao
				nAlEmissao := SE1->(FieldGet(FieldPos("E1_ALEMIS"+aSD2Vend[nCntFor,13])))
			Else
				nAlEmissao := SA3->A3_ALEMISS
			EndIf
			If SE1->(FieldPos("E1_ALBAIX"+aSD2Vend[nCntFor,13]))<>0 //Nao criar no dicionario padrao
				nAlBaixa := SE1->(FieldGet(FieldPos("E1_ALBAIX"+aSD2Vend[nCntFor,13])))
			Else
				nAlBaixa := SA3->A3_ALBAIXA
			EndIf

			If ( SA3->A3_FRETE == "N" )
				nBaseSE1 -=  nRatFrete
				nBaseSE1 +=  nRatIcmFre
			Endif
			If ( SA3->A3_IPI   == "N" )
				nBaseSE1 -= ( aSD2Vend[nCntFor,4] + nRatIpiFre )
			EndIf
			If !aSD2Vend[nCntFor,12]
				If ( SA3->A3_ICM   == "N" )
					nBaseSE1 -= aSD2Vend[nCntFor,3]
				EndIf
			Else
				If ( SA3->A3_ISS == "N" )
					nBaseSE1 -= ( aSD2Vend[nCntFor,3] )
				EndIf
			EndIf				
			If ( SA3->A3_ICMSRET == "N" )
				nBaseSE1 -= ( aSD2Vend[nCntFor,8] )
			EndIf
			If SA3->(FieldPos("A3_ACREFIN")) != 0  // Acrescimo Financeiro
				If ( SA3->A3_ACREFIN == "N" ) .and. aSD2Vend[nCntFor,10] > 0
					nBaseSE1 -= ( aSD2Vend[nCntFor,10] )
				EndIf
			Endif
			nPos := aScan(aBaseSE1,{|x| x[1] == aSD2Vend[nCntFor,1]})
			If Alltrim(SE1->E1_Hist) <> "VENDA EM DINHEIRO"
				nBaseBaix := Round(nBaseSE1*nAlBaixa/100,nDecimal)	 	// Base da Comissao na Baixa
			ELSE
				nBaseBaix:= 0
			Endif
			nBaseEmis := nBaseSE1-nBaseBaix                           		// Base da Comissao na Emissao
			If ( nAlEmissao==0 .AND.  Alltrim(SE1->E1_Hist) <> "VENDA EM DINHEIRO" )
				nBaseEmis := 0
			EndIf
			nVlrEmis  := Round(nBaseEmis*aSD2Vend[nCntFor,9]/100,nDecimal)	// Valor da Comissao na Emissao
			nVlrBaix  := Round(nBaseBaix*aSD2Vend[nCntFor,9]/100,nDecimal)	// Valor da Comissao na Baixa
			If ( lPerComis )
				nPerComis := 0
			EndIf
			If ( Empty(nRegDevol) .Or. nRegDevol == aSD2Vend[nCntFor,11] )
				If ( nPos == 0 )
					aadd(aBaseSE1,{ aSD2Vend[nCntFor,1] ,;
						nBaseSE1  				,;
						nBaseEmis 				,;
						nBaseBaix 				,;
						nVlrEmis  				,;
						nVlrBaix				,;
						nPerComis})
				Else
					aBaseSE1[nPos,2] += nBaseSE1
					aBaseSE1[nPos,3] += nBaseEmis
					aBaseSE1[nPos,4] += nBaseBaix
					aBaseSE1[nPos,5] += nVlrEmis
					aBaseSE1[nPos,6] += nVlrBaix
					aBaseSE1[nPos,7] := nPerComis
				EndIf	
			EndIf
		Next nCntFor
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calculo da comissao pelas parcelas.                          ³
		//³                                                              ³
		//³1)O SE3 ‚ gravado por parcela e nao pela nota. assim e'neces- ³
		//³ssario calcular a base da comissao para a parcela em questao. ³
		//³                                                              ³
		//³2)Aqui deve-se tomar o maximo cuidado com a Condi‡Æo de pagto ³
		//³pois se o Icms Solidario ou o Ipi for separado de alguma par- ³
		//³cela deve-se considera esta separacao para calcular-se a me-  ³
		//³lhor propor‡ao possivel para a base da parcela.               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nMaxFor := Len(aBaseSE1)
		For nCntFor := 1 To nMaxFor
			nProp   := 0
			nVlrFat := xMoeda(SF2->F2_VALFAT,SE1->E1_MOEDA,1,SE1->E1_EMISSAO)
			nVlrTit := SE1->E1_VLCRUZ
			dbSelectArea("SA3")
			dbSetOrder(1)
			MsSeek(xFilial()+aBaseSE1[nCntFor,1])
			If ( SA3->A3_IPI == "N" .Or. SA3->A3_ICMSRET == "N" )
				If ( (SE4->E4_IPI == "S" .Or. SE4->E4_SOLID=="S") .And. SE1->E1_PARCELA $ cPrimParc )
					nVlrTit := 0
					nVlrFat := 0
				EndIf
				If ( SE4->E4_IPI == "S" .And. !SE1->E1_PARCELA $ cPrimParc)
					nVlrFat -= ( SF2->F2_VALIPI )
				EndIf
				If ( SE4->E4_SOLID == "S" .And. !SE1->E1_PARCELA $ cPrimParc)
					nVlrFat -= (nSf2IcmRet)
				EndIf
				If ( SE4->E4_IPI == "J" .And. SE1->E1_PARCELA $ cPrimParc)
					nVlrFat -= ( SF2->F2_VALIPI )
					nVlrTit -= ( SF2->F2_VALIPI )
				EndIf
				If ( SE4->E4_IPI == "J" .And. !(SE1->E1_PARCELA $ cPrimParc ))
					nVlrFat -= ( SF2->F2_VALIPI )
				EndIf
				If ( SE4->E4_SOLID == "J" .And. SE1->E1_PARCELA $ cPrimParc )
					nVlrFat -= (nSf2IcmRet)
					nVlrTit -= (nSf2IcmRet)
				EndIf
				If ( SE4->E4_SOLID == "J" .And. !(SE1->E1_PARCELA $ cPrimParc) )
					nVlrFat -= (nSf2IcmRet)
				EndIf
			EndIf
			If ( nVlrTit > 0 )
				nProp := nVlrFat / nVlrTit
			Else
				nProp := 0
			EndIf	
			If (nProp != 0 )
				nBaseSE1 := NoRound(aBaseSE1[nCntFor,2]/nProp,nDecimal)
				nBaseEmis:= NoRound(aBaseSE1[nCntFor,3]/nProp,nDecimal)
				nBaseBaix:= NoRound(aBaseSE1[nCntFor,4]/nProp,nDecimal)
				nVlrEmis := Round(aBaseSE1[nCntFor,5]/nProp,nDecimal)
				nVlrBaix := Round(aBaseSE1[nCntFor,6]/nProp,nDecimal)
			Else
				nBaseSE1 := 0
				nBaseEmis:= 0
				nBaseBaix:= 0
				nVlrEmis := 0
				nVlrBaix := 0
			EndIf
			If ( SE1->E1_PARCELA $ cPrimParc .And. nBaseSE1 != 0 )
				//--> Calculo da Proporcao para a Base da Comissao
				nBaseDif := Round(nBaseSE1 * nProp,nDecimal)
				nBaseDif := aBaseSE1[nCntFor,2]-nBaseDif
				aBaseSE1[nCntFor,2] := nBaseSE1+nBaseDif
				//--> Calculo da Proporcao para a Base da Comissao pela Emissao

				nBaseDif := Round(nBaseEmis * nProp,nDecimal)
				nBaseDif := aBaseSE1[nCntFor,3]-nBaseDif
				aBaseSE1[nCntFor,3] := nBaseEmis+nBaseDif
				//--> Calculo da Proporcao para a Base da Comissao pela Baixa

				nBaseDif := Round(nBaseBaix * nProp,nDecimal)
				nBaseDif := aBaseSE1[nCntFor,4]-nBaseDif
				aBaseSE1[nCntFor,4] := nBaseBaix+nBaseDif
				//--> Calculo da Proporcao para o Valor da Comissao pela Emissao

				nBaseDif := Round(nVlrEmis * nProp,nDecimal)
				nBaseDif := aBaseSE1[nCntFor,5]-nBaseDif
				aBaseSE1[nCntFor,5] := nVlrEmis+nBaseDif
				//--> Calculo da Proporcao para o Valor da Comissao pela Baixa

				nBaseDif := Round(nVlrBaix * nProp,nDecimal)
				nBaseDif := aBaseSE1[nCntFor,6]-nBaseDif
				aBaseSE1[nCntFor,6] := nVlrBaix+nBaseDif
			Else
				aBaseSE1[nCntFor,2] := nBaseSE1
				aBaseSE1[nCntFor,3] := nBaseEmis
				aBaseSE1[nCntFor,4] := nBaseBaix
				aBaseSE1[nCntFor,5] := nVlrEmis
				aBaseSE1[nCntFor,6] := nVlrBaix
			EndIf
		Next nCntFor
	Else
		If ( lQuery .And. Select(cAliasSD2)<>0 )
			dbSelectArea(cAliasSD2)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	EndIf
EndIf
If ( SE1->E1_TIPO $ MV_CRNEG )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a filial de origem das notas de devolucao de Venda  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SF1->(FieldPos("F1_FILORIG")) > 0
		cFilialSF1 := SF1->F1_FILORIG
	EndIf	
	If SD1->(FieldPos("D1_FILORIG")) > 0
		cFilialSD1 := SD1->D1_FILORIG
	EndIf
	If Year(SE1->E1_EMISSAO)<2001
		cSerie := If(Empty(SE1->E1_SERIE),SE1->E1_PREFIXO,SE1->E1_SERIE)
	Else
		cSerie := SE1->E1_SERIE
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona Registros                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SF1")
	dbSetOrder(1)
	If (!MsSeek(cFilialSF1+SE1->E1_NUM+cSerie+SE1->E1_CLIENTE+SE1->E1_LOJA,.F.))
		lContinua := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calculo do Estorno da Comissao.                              ³
	//³                                                              ³
	//³1) Localiza-se a Nota Original                                ³
	//³                                                              ³
	//³2) Calcula a Comissao para a Nota Original                    ³
	//³                                                              ³
	//³3) Faz a Proporcao entre os valor da Mercadoria e os valores  ³
	//³   da comissao.                                               ³
	//³                                                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SD1")
	dbSetOrder(1)
	#IFDEF TOP
		If ( TcSrvType()!="AS/400" )
			SD1->(dbCommit())
			cAliasSD1 := "BFA440COMIS"
			lQuery := .T.
			cQuery := ""
			For nCntFor := 1 To Len(aStruSD1)
				cQuery += ","+aStruSD1[nCntFor][1]
			Next nCntFor
			cQuery := "SELECT "+SubStr(cQuery,2)
			cQuery += "FROM "+RetSqlName("SD1")+" SD1 "
			cQuery += "WHERE SD1.D1_FILIAL='"+cFilialSD1+"' AND "
			cQuery += "SD1.D1_DOC='"+SE1->E1_NUM+"' AND "
			cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
			cQuery += "SD1.D1_FORNECE='"+SE1->E1_CLIENTE+"' AND "
			cQuery += "SD1.D1_LOJA='"+SE1->E1_LOJA+"' AND "
			If cPaisLoc<>"BRA"
				cQuery += "SD1.D_E_L_E_T_<>'*' "
			Else
				cQuery += "SD1.D_E_L_E_T_<>'*' AND "
				cQuery += "SD1.D1_ITEMORI<>'"+Space(Len(SD1->D1_ITEMORI))+"' "
			EndIF
			cQuery += "ORDER BY "+SqlOrder(SD1->(IndexKey()))
			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1,.T.,.T.)
			lContinua := !(cAliasSD1)->(Eof())
			For nCntFor := 1 To Len(aStruSD1)
				If ( aStruSD1[nCntFor][2]!="C" )
					TcSetField(cAliasSD1,aStruSD1[nCntFor][1],aStruSD1[nCntFor][2],aStruSD1[nCntFor][3],aStruSD1[nCntFor][4])
				EndIf
			Next nCntFor
		Else
	#ENDIF
		If (!MsSeek(cFilialSD1+SE1->E1_NUM+cSerie+SE1->E1_CLIENTE+SE1->E1_LOJA,.F.))
			lContinua := .F.
		EndIf
		#IFDEF TOP
		EndIf
		#ENDIF
	aSemNota	:=	{}
	While ( !Eof() .And. xFilial("SD1")  == (cAliasSD1)->D1_FILIAL  .And.;
			SF1->F1_DOC 	 == (cAliasSD1)->D1_DOC	  .And.;
			SF1->F1_SERIE	 == (cAliasSD1)->D1_SERIE   .And.;
			SF1->F1_FORNECE == (cAliasSD1)->D1_FORNECE .And.;
			SF1->F1_LOJA	 == (cAliasSD1)->D1_LOJA )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Localiza a Nota de Saida - Item                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SD2")
		dbSetOrder(3)
		MsSeek(cFilialSD2+(cAliasSD1)->D1_NFORI+(cAliasSD1)->D1_SERIORI+(cAliasSD1)->D1_FORNECE+(cAliasSD1)->D1_LOJA+(cAliasSD1)->D1_COD+AllTrim((cAliasSD1)->D1_ITEMORI))
		If !SD2->(FOUND())
			//Carrega os items que nao tem nota original
			AAdd(aSemNota,(cAliasSD1)->D1_ITEM)
		Endif
		aBaseSD1 := {} // Inicializa Bases do Item
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona no Nota Fiscal de Saida - Cabecalho                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SF2")
		dbSetOrder(1)
		MsSeek(cFilialSF2+SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA)
		cPrefixo := &(GetMV("MV_1DUPREF"))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona no Titulo Financeiro                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE1")
		dbSetOrder(2)
		MsSeek(xFilial("SE1")+SF2->F2_CLIENTE+SF2->F2_LOJA+cPrefixo+SD2->D2_DOC)
		While ( !Eof() .And. xFilial("SE1")  == SE1->E1_FILIAL .And.;
				SE1->E1_CLIENTE == SF2->F2_CLIENTE .And.;
				SE1->E1_LOJA    == SF2->F2_LOJA .And.;
				SE1->E1_PREFIXO == cPrefixo .And.;
				SE1->E1_NUM		 == SF2->F2_DOC )
			If ( SE1->E1_TIPO $ MVNOTAFIS+cEspSes )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o Valor da Comissao para a Parcela                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aBaseNCC := Fe440Comis(SE1->(Recno()),.F.,.T.,SD2->(RecNo()))
				For nCntFor := 1 To Len(aBaseNCC)
					cVendedor := aBaseNCC[nCntFor,1]
					nPos := aScan(aBaseSD1,{|x| x[1]==cVendedor})
					If ( nPos == 0 )
						aadd(aBaseSD1,{ 	aBaseNCC[nCntFor,1],;
							aBaseNCC[nCntFor,2],;
							aBaseNCC[nCntFor,3],;
							aBaseNCC[nCntFor,4],;
							aBaseNCC[nCntFor,5],;
							aBaseNCC[nCntFor,6],;
							aBaseNCC[nCntFor,7]})
					Else
						aBaseSD1[nPos,2] += aBaseNCC[nCntFor,2]
						aBaseSD1[nPos,3] += aBaseNCC[nCntFor,3]
						aBaseSD1[nPos,4] += aBaseNCC[nCntFor,4]
						aBaseSD1[nPos,5] += aBaseNCC[nCntFor,5]
						aBaseSD1[nPos,6] += aBaseNCC[nCntFor,6]
					EndIf
				Next nCntFor
			EndIf
			dbSelectArea("SE1")
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aqui eh proporcionalizado as Bases da nota de saida com o item devol.  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nCntFor := 1 To Len(aBaseSD1)
			aBaseSD1[nCntFor,2] := NoRound(aBaseSD1[nCntFor,2]*((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC)/SD2->D2_TOTAL,nDecimal)
			aBaseSD1[nCntFor,3] := NoRound(aBaseSD1[nCntFor,3]*((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC)/SD2->D2_TOTAL,nDecimal)
			aBaseSD1[nCntFor,4] := NoRound(aBaseSD1[nCntFor,4]*((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC)/SD2->D2_TOTAL,nDecimal)
			aBaseSD1[nCntFor,5] := NoRound(aBaseSD1[nCntFor,5]*((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC)/SD2->D2_TOTAL,nDecimal)
			aBaseSD1[nCntFor,6] := NoRound(aBaseSD1[nCntFor,6]*((cAliasSD1)->D1_TOTAL-(cAliasSD1)->D1_VALDESC)/SD2->D2_TOTAL,nDecimal)
		Next nCntFor
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Aqui eh somado as bases ja calculadas e como a NCC estorna os valores   ³
		//³na emissao, adiciona-se a base da baixa na base da emissao.             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nCntFor := 1 To Len(aBaseSD1)
			cVendedor := aBaseSD1[nCntFor,1]
			nPos := aScan(aBAseSE1,{|x| x[1] == cVendedor })
			If ( nPos == 0 )
				aadd(aBaseSE1,{ 	aBaseSD1[nCntFor,1],;
					aBaseSD1[nCntFor,2],;
					aBaseSD1[nCntFor,3]+aBaseSD1[nCntFor,4],0,;
					aBaseSD1[nCntFor,5]+aBaseSD1[nCntFor,6],0,0})
			Else
				aBaseSE1[nPos,2] += aBaseSD1[nCntFor,2]
				aBaseSE1[nPos,3] += aBaseSD1[nCntFor,3]+aBaseSD1[nCntFor,4]
				aBaseSE1[nPos,5] += aBaseSD1[nCntFor,5]+aBaseSD1[nCntFor,6]
			EndIf
		Next nCntFor
		dbSelectArea(cAliasSD1)
		dbSkip()
	EndDo
	If ( (Empty(aBaseSE1).Or. Len(aSemNota) > 0) .And. lRefaz .And. cPaisLoc<>"BRA")
		dbSelectArea("SE1")
		MsGoto(nRegistro)
		cSerie := If(Empty(SE1->E1_SERIE),SE1->E1_PREFIXO,SE1->E1_SERIE)
		dbSelectArea("SF1")
		dbSetOrder(1)
		If (!MsSeek(cFilialSF1+SE1->E1_NUM+cSerie+SE1->E1_CLIENTE+SE1->E1_LOJA,.F.))
			lContinua := .F.
		EndIf
		dbSelectArea("SE4")
		dbSetOrder(1)
		If (!MsSeek(cFilialSE4+SF1->F1_COND))
			lContinua := .F.
		EndIf
		dbSelectArea("SD1")
		dbSetOrder(1)
		#IFDEF TOP
			If ( TcSrvType()!="AS/400" )
				lQuery := .T.
				cAliasDev := "FA440COMIS"
				cAliasSF4 := "FA440COMIS"
				cQuery := ""
				For nCntFor := 1 To Len(aStruSD1)
					cQuery += ","+aStruSD1[nCntFor][1]
				Next nCntFor
				For nCntFor := 1 To Len(aStruSF4)
					cQuery += ","+aStruSF4[nCntFor][1]
				Next nCntFor
				cQuery := "SELECT SD1.R_E_C_N_O_ SD1RECNO,"+SubStr(cQuery,2)
				cQuery += "FROM "+RetSqlName("SD1")+" SD1,"+RetSqlName("SF4")+" SF4 "
				cQuery += "WHERE SD1.D1_FILIAL='"+cFilialSD1+"' AND "
				cQuery += "SD1.D1_DOC='"+SE1->E1_NUM+"' AND "
				cQuery += "SD1.D1_SERIE='"+cSerie+"' AND "
				cQuery += "SD1.D1_FORNECE='"+SE1->E1_CLIENTE+"' AND "
				cQuery += "SD1.D1_LOJA='"+SE1->E1_LOJA+"' AND "
				cQuery += "SD1.D_E_L_E_T_<>'*' AND "
				cQuery += "SF4.F4_FILIAL='"+cFilialSF4+"' AND "
				cQuery += "SF4.F4_CODIGO=SD1.D1_TES AND "
				cQuery += "SF4.D_E_L_E_T_<>'*' "
				cQuery += "ORDER BY "+SqlOrder(SD1->(IndexKey()))
				cQuery := ChangeQuery(cQuery)

				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasDev)
				lContinua := !(cAliasDev)->(Eof())
				For nCntFor := 1 To Len(aStruSD1)
					If ( aStruSD1[nCntFor][2]!= "C" )
						TcSetField(cAliasDev,aStruSD1[nCntFor][1],aStruSD1[nCntFor][2],aStruSD1[nCntFor][3],aStruSD1[nCntFor][4])
					EndIf
				Next nCntFor
				For nCntFor := 1 To Len(aStruSF4)
					If ( aStruSF4[nCntFor][2]!="C" )
						TcSetField(cAliasSF4,aStruSF4[nCntFor][1],aStruSF4[nCntFor][2],aStruSD1[nCntFor][3],aStruSF4[nCntFor][4])
					EndIf
				Next nCntFor
			Else
		#ENDIF	
			If (!MsSeek(cFilialSD1+SE1->E1_NUM+cSerie+SE1->E1_CLIENTE+SE1->E1_LOJA,.F.))
				lContinua := .F.
			EndIf

			#IFDEF TOP
			EndIf	
			#ENDIF
		If ( lContinua )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calculo da comissao por item de nota fiscal 			     ³
			//³																 ³
			//³1)O Valor do icms s/ frete e adiciona ao campo F2_VALICM, por ³
			//³esta razao deve-se somar o vlr do icms dos itens e subtrair   ³
			//³do total de icms (F2_VALICM) para apurar-se o vlr icms s/frete³
			//³																 ³
			//³2)O mesmo ocorre para o valor do IPI sobre frete, Por esta ra-³
			//³zao e' calculado o valor do IPI sobre frete do item multipli- ³
			//³cando-se o valor do frete do item pelo % de ipi do item. 	 ³
			//³																 ³
			//³3)O Valor do Icms Retido pode nÆo estar no total da nota (F2_-³
			//³VALBRUT) por isto deve-se considerar o campo (D2_ICMSRET).	 ³
			//³																 ³
			//³4)O percentual da comissao dever ser considerado para cada i- ³
			//³tem de nota fiscal pois ela pode ser diferente entre eles. O  ³
			//³percentual gravado no E1_COMIS ‚ sempre um valor aproximado e ³
			//³nao deve ser considerado ser houver nota fiscal para o titulo.³
			//³																 ³
			//³5)A Base da Comissao ‚ o valor da mercadoria + o valor do ipi ³
			//³+ o valor das despesas acessorias +  o icms solidario. Como e'³
			//³por item deve-se conhece-lo pelo item a item.				 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nTotal	  := 0
			nFrete	  := (SF1->F1_FRETE + SF1->F1_SEGURO + SF1->F1_DESPESA)
			nIcmFrete  := 0
			nSF2IcmRet :=0
			While ( !Eof() .And. (cAliasDev)->D1_FILIAL == cFilialSD1 .And.;
					(cAliasDev)->D1_DOC 	 == SE1->E1_NUM .And.;
					(cAliasDev)->D1_SERIE	 == cSerie .And.;
					(cAliasDev)->D1_FORNECE  == SE1->E1_CLIENTE .And.;
					(cAliasDev)->D1_LOJA	 == SE1->E1_LOJA	)

				If Ascan(aSemNota,(cAliasDev)->D1_ITEM) ==0
					(cAliasDev)->(DbSkip())
					Loop
				EndIf

				If ( !lQuery )
					dbSelectArea("SF4")
					dbSetOrder(1)
					MsSeek(cFilialSF4+(cAliasDev)->D1_TES)
				EndIf
				cVend := "1"

				cVendedor := SF1->F1_VEND1

				nImp := 0				
				If cPaisLoc <> "BRA"
					SA3->(DbSetOrder(1))
					SA3->(DbSeek(xFilial()+cVendedor))
					aImp := TesImpInf(SD1->D1_TES)			
					cImp := IIf(SA3->(FieldPos("A3_COMIMP")) >0,SA3->A3_COMIMP,"N")
					nPerComis := SA3->A3_COMIS
					For nX :=1 to Len(aImp)
						If (cImp+aImp[nX][3] == "S1")
							nImp += SD1->(FieldGet(FieldPos(aImp[nX][2])))
						ElseIf (cImp+aImp[nX][3] == "N2")
							nImp -= SD1->(FieldGet(FieldPos(aImp[nX][2]))	)
						Endif
					Next	
				EndIf

				If ( !Empty(cVendedor) .And. (cAliasSF4)->F4_DUPLIC == "S" )
					aadd(aSD2Vend,{ cVendedor,;
						(cAliasDev)->D1_TOTAL+nImp,;
						0,;
						0,;
						0,;
						(cAliasSF4)->F4_INCIDE,;
						(cAliasSF4)->F4_IPIFRET,;
						0,;
						nPerComis,;
						0,;
						If(lQuery,(cAliasDev)->SD1RECNO,(cAliasDev)->(RecNo()))})
				EndIf

				nTotal	  += (cAliasDev)->D1_TOTAL
				dbSelectArea(cAliasDev)
				dbSkip()
			EndDo
			If ( lQuery )
				dbSelectArea(cAliasDev)
				dbCloseArea()
				dbSelectArea("SD1")
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calculo da comissao pela nota.				 ³
			//³								 ³
			//³1)Apos calculado as bases de cada vendedor por item de nota   ³
			//³deve-se aglutina-las formando uma unica base para toda a nota ³
			//³fiscal.							 ³
			//³								 ³
			//³2)Como os valores serao aglutinados pode-se haver perda de de-³
			//³cimais por isto deve-se haver um controle para que a perda se-³
			//³ja adicionada a primeira parcela da nota. 			 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nMaxFor := Len(aSD2Vend)
			nPerComis:=0
			lPerComis:=.F.
			For nCntFor := 1 To nMaxFor
				If ( !lPeperCom )
					If ( !lPerComis )
						If ( nPerComis == 0 .And. nCntFor == 1 )
							nPerComis := aSD2Vend[nCntFor][9]
						EndIf
						If ( nPerComis != aSD2Vend[nCntFor][9] )
							lPerComis := .T.
						EndIf
					EndIf
				Else
					nPerComis := ExecBlock("FIN440PE",.F.,.F.,{aSD2Vend[nCntFor][1]})
					If ( ValType(nPerComis)<>"N" )
						nPerComis := aSD2Vend[nCntFor][9]
						lPerComis := .F.
					Else
						lPerComis := .T.
					EndIf
				EndIf
				If ( SE1->E1_PARCELA $ cPrimParc )
					nBaseDif  := NoRound(nFrete*(1-(aSD2Vend[nCntFor,2]/nTotal)),nDecimal)
					nBaseDif  := nFrete - nBaseDif
					nRatFrete := nBaseDif
				Else
					nRatFrete := NoRound(nFrete*aSD2Vend[nCntFor,2]/nTotal,nDecimal)
				EndIf
				nRatIpiFre:= 0
				nRatIcmFre:= 0
				nBaseSE1  := 0
				nPos		 := 0
				If ( aSD2Vend[nCntFor,7] $ "S " )
					nRatIpiFre:= NoRound(nRatFrete*aSD2Vend[nCntFor,5]/100,nDecimal)
				Else
					nRatIpiFre:= 0
				EndIf
				nRatIcmFre:= NoRound(nIcmFrete*aSD2Vend[nCntFor,2]/nTotal,nDecimal)
				nBaseSE1  := aSD2Vend[nCntFor,2]+aSD2Vend[nCntFor,4]+aSD2Vend[nCntFor,8]+nRatFrete+nRatIpiFre
				dbSelectArea("SA3")
				dbSetOrder(1)
				MsSeek(xFilial()+aSD2Vend[nCntFor,1])

				nAlEmissao := SA3->A3_ALEMISS
				nAlBaixa := SA3->A3_ALBAIXA

				If ( SA3->A3_FRETE == "N" )
					nBaseSE1 -= ( nRatFrete )
				Endif
				If SA3->(FieldPos("A3_ACREFIN")) != 0  // Acrescimo Financeiro
					If ( SA3->A3_ACREFIN == "N" ) .and. aSD2Vend[nCntFor,10] > 0
						nBaseSE1 -= ( aSD2Vend[nCntFor,10] )
					EndIf
				Endif
				nPos := aScan(aBaseSE1,{|x| x[1] == aSD2Vend[nCntFor,1]})
				If Alltrim(SE1->E1_Hist) <> "VENDA EM DINHEIRO"
					nBaseBaix := Round(nBaseSE1*nAleBaixa/100,nDecimal) 	// Base da Comissao na Baixa
				Else
					nBaseBaix:= 0
				EndIf
				nBaseEmis := nBaseSE1-nBaseBaix											// Base da Comissao na Emissao
				nVlrEmis  := Round(nBaseEmis*aSD2Vend[nCntFor,9]/100,nDecimal) // Valor da Comissao na Emissao
				nVlrBaix  := Round(nBaseBaix*aSD2Vend[nCntFor,9]/100,nDecimal) // Valor da Comissao na Baixa
				If ( lPerComis )
					nPerComis := 0
				EndIf
				If ( Empty(nRegDevol) .Or. nRegDevol == aSD2Vend[nCntFor,11] )
					If ( nPos == 0 )
						aadd(aBaseSE1,{ aSD2Vend[nCntFor,1] ,;
							nBaseSE1				,;
							nBaseEmis				,;
							nBaseBaix				,;
							nVlrEmis				,;
							nVlrBaix				,;
							nPerComis				})
					Else                	
						aBaseSE1[nPos,2] += nBaseSE1
						aBaseSE1[nPos,3] += nBaseEmis
						aBaseSE1[nPos,4] += nBaseBaix
						aBaseSE1[nPos,5] += nVlrEmis
						aBaseSE1[nPos,6] += nVlrBaix
						aBaseSE1[nPos,7] := nPerComis
					EndIf
				EndIf
			Next nCntFor
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calculo da comissao pelas parcelas.				 ³
			//³								 ³
			//³1)O SE3 ‚ gravado por parcela e nao pela nota. assim e'neces- ³
			//³ssario calcular a base da comissao para a parcela em questao. ³
			//³								 ³
			//³2)Aqui deve-se tomar o maximo cuidado com a Condi‡Æo de pagto ³
			//³pois se o Icms Solidario ou o Ipi for separado de alguma par- ³
			//³cela deve-se considera esta separacao para calcular-se a me-  ³
			//³lhor propor‡ao possivel para a base da parcela.		 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nMaxFor := Len(aBaseSE1)
			For nCntFor := 1 To nMaxFor
				nProp   := 0
				nVlrFat := xMoeda(SF1->F1_VALBRUT,SE1->E1_MOEDA,1,SE1->E1_EMISSAO)
				nVlrTit := SE1->E1_VLCRUZ
				dbSelectArea("SA3")
				dbSetOrder(1)
				MsSeek(xFilial()+aBaseSE1[nCntFor,1])
				If ( nVlrTit > 0 )
					nProp := nVlrFat / nVlrTit
				Else
					nProp := 0
				EndIf
				If (nProp != 0 )
					nBaseSE1 := NoRound(aBaseSE1[nCntFor,2]/nProp,nDecimal)
					nBaseEmis:= NoRound(aBaseSE1[nCntFor,3]/nProp,nDecimal)
					nBaseBaix:= NoRound(aBaseSE1[nCntFor,4]/nProp,nDecimal)
					nVlrEmis := Round(aBaseSE1[nCntFor,5]/nProp,nDecimal)
					nVlrBaix := Round(aBaseSE1[nCntFor,6]/nProp,nDecimal)
				Else
					nBaseSE1 := 0
					nBaseEmis:= 0
					nBaseBaix:= 0
					nVlrEmis := 0
					nVlrBaix := 0
				EndIf
				If ( SE1->E1_PARCELA $ cPrimParc .And. nBaseSE1 != 0 )
					//--> Calculo da Proporcao para a Base da Comissao
					nBaseDif := Round(nBaseSE1 * nProp,nDecimal)
					nBaseDif := aBaseSE1[nCntFor,2]-nBaseDif
					aBaseSE1[nCntFor,2] := nBaseSE1+nBaseDif
					//--> Calculo da Proporcao para a Base da Comissao pela Emissao

					nBaseDif := Round(nBaseEmis * nProp,nDecimal)
					nBaseDif := aBaseSE1[nCntFor,3]-nBaseDif
					aBaseSE1[nCntFor,3] := nBaseEmis+nBaseDif
					//--> Calculo da Proporcao para a Base da Comissao pela Baixa

					nBaseDif := Round(nBaseBaix * nProp,nDecimal)
					nBaseDif := aBaseSE1[nCntFor,4]-nBaseDif
					aBaseSE1[nCntFor,4] := nBaseBaix+nBaseDif
					//--> Calculo da Proporcao para o Valor da Comissao pela Emissao

					nBaseDif := Round(nVlrEmis * nProp,nDecimal)
					nBaseDif := aBaseSE1[nCntFor,5]-nBaseDif
					aBaseSE1[nCntFor,5] := nVlrEmis+nBaseDif
					//--> Calculo da Proporcao para o Valor da Comissao pela Baixa

					nBaseDif := Round(nVlrBaix * nProp,nDecimal)
					nBaseDif := aBaseSE1[nCntFor,6]-nBaseDif
					aBaseSE1[nCntFor,6] := nVlrBaix+nBaseDif
				Else
					aBaseSE1[nCntFor,2] := nBaseSE1
					aBaseSE1[nCntFor,3] := nBaseEmis
					aBaseSE1[nCntFor,4] := nBaseBaix
					aBaseSE1[nCntFor,5] := nVlrEmis
					aBaseSE1[nCntFor,6] := nVlrBaix
				EndIf
			Next nCntFor
		Else
			If ( lQuery .And. Select(cAliasDev)<>0 )
				dbSelectArea(cAliasDev)
				dbCloseArea()
				dbSelectArea("SE1")
			EndIf
		EndIf
	Endif	
	If ( lQuery .And. Select(cAliasDev)<>0 )
		dbSelectArea(cAliasDev)
		dbCloseArea()
		dbSelectArea("SE1")
	EndIf
	If ( lQuery )
		dbSelectArea(cAliasSD1)
		dbCloseArea()
		dbSelectArea("SD1")
	EndIf
Else
	If ( Empty(aBaseSE1) .And. lRefaz )
		cVend := "1"
		For nCntFor := 1 To nVend
			cVendedor := SE1->(FieldGet(SE1->(FieldPos("E1_VEND"+cVend))))
			nPerComis := SE1->(FieldGet(SE1->(FieldPos("E1_COMIS"+cVend))))
			dbSelectArea("SA3")
			dbSetOrder(1)
			MsSeek(xFilial()+cVendedor)
			If SE1->(FieldPos("E1_ALEMIS"+cVend))<>0//Nao criar no dicionario padrao
				nAlEmissao := SE1->(FieldGet(FieldPos("E1_ALEMIS"+cVend)))
			Else
				nAlEmissao := SA3->A3_ALEMISS
			EndIf
			If SE1->(FieldPos("E1_ALBAIX"+cVend))<>0//Nao criar no dicionario padrao
				nAlBaixa := SE1->(FieldGet(FieldPos("E1_ALBAIX"+cVend)))
			Else
				nAlBaixa := SA3->A3_ALBAIXA
			EndIf

			If ( !Empty(cVendedor) )
				If Alltrim(SE1->E1_Hist) <> "VENDA EM DINHEIRO"			 //Adriano - Comissoes			
					nBaseEmis := Round(SE1->E1_VLCRUZ*(nAlEmissao/100),nDecimal)
					nBaseBaix := Round(SE1->E1_VLCRUZ*(nAlBaixa/100),nDecimal)
				Else
					nBaseEmis := SE1->(FieldGet(SE1->(FieldPos("E1_BASCOM"+cVend))))
					nBaseBaix := nBaseEmis
				Endif	
				nVlrEmis  := Round(nBaseEmis * (nPerComis/100),nDecimal)
				nVlrBaix  := Round(nBaseBaix * (nPerComis/100),nDecimal)
				aadd(aBaseSE1,{ cVendedor,;
					SE1->E1_VLCRUZ,;
					nBaseEmis,;
					nBaseBaix,;
					nVlrEmis,;
					nVlrBaix,;
					nPerComis})
			EndIf
			cVend := Soma1(cVend,1)
		Next nCntFor
	EndIf
	If ( lGrava .And. lRefaz )
		dbSelectArea("SE1")
		RecLock("SE1")
		cVend := "1"
		For nCntFor := 1 To Len(aBaseSE1)
			dbSelectArea("SA3")
			dbSetOrder(1)
			MsSeek(xFilial()+aBaseSE1[nCntFor,1])
			dbSelectArea("SE1")
			If ( FieldGet(FieldPos("E1_VEND"+cVend)) == aBaseSE1[nCntFor,1] )
				FieldPut(FieldPos("E1_BASCOM"+cVend),aBaseSE1[nCntFor,4])
				If ( aBaseSE1[nCntFor,4] != 0 )
					FieldPut(FieldPos("E1_COMIS"+cVend),aBaseSE1[nCntFor,7])
				Endif
				FieldPut(FieldPos("E1_VALCOM"+cVend),aBaseSE1[nCntFor,5])
			Else
				If ( SE1->(FieldPos("E1_VEND"+cVend)) != 0 )
					nCntFor--	
				EndIf
			EndIf
			cVend := Soma1(cVend,1)
		Next nCntFor
		MsUnlock()
	EndIf
	If ( Empty(aBaseSE1) )
		cVend := "1"
		For nCntFor := 1 To nVend
			cVendedor := SE1->(FieldGet(SE1->(FieldPos("E1_VEND"+cVend))))
			nPerComis := SE1->(FieldGet(SE1->(FieldPos("E1_COMIS"+cVend))))
			dbSelectArea("SA3")
			dbSetOrder(1)
			MsSeek(xFilial()+cVendedor)
			If ( !Empty(cVendedor) )
				nBaseSE1  := 0
				nBaseEmis := 0
				nBaseBaix := SE1->(FieldGet(SE1->(FieldPos("E1_BASCOM"+cVend))))
				nVlrEmis  := 0
				nVlrBaix  := SE1->(FieldGet(SE1->(FieldPos("E1_BASCOM"+cVend)))) * (nPerComis/100)
				// --> Quando o percentual da comissao estiver no produto o sistema
				// --> arredonda o percentual se for pago na baixa, caso haja muita
				// --> distorcao de valores deve-se alterar o numero de casas
				// --> decimais do campo E1_COMIS1..E1_COMIS(n)
				aadd(aBaseSE1,{ cVendedor,;
					nBaseSE1,;
					nBaseEmis,;
					nBaseBaix,;
					nVlrEmis,;
					nVlrBaix,;
					nPerComis })
			EndIf
			cVend := Soma1(cVend,1)
		Next nCntFor
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a Integridade dos dados de Entrada                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaSE1)
RestArea(aAreaSE4)
RestArea(aAreaSF1)
RestArea(aAreaSF2)
RestArea(aAreaSF4)
RestArea(aAreaSD1)
RestArea(aAreaSD2)
RestArea(aAreaSA3)
RestArea(aArea)

Return(aBaseSE1)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fe440CntVe³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de Contagem do Numero de Vendedores Utilizados.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Numero de Vendedores                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINE440                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function fe440CntVen()

Local cCnt     := "1"
Local aStruct  := SE1->(dbStruct())
Local lContinua:= .T.
Static nVend

If ( Empty(nVend) )
	nVend := 0
	While ( lContinua )
		If ( aScan(aStruct,{|x| Trim(x[1]) == "E1_VEND"+cCnt}) != 0 )
			nVend++
			cCnt := Soma1(cCnt,1)
		Else
			lContinua := .F.
		EndIf
	EndDo
	nVend := Max(nVend,5)
EndIf
Return(nVend)

User Function fe440SA1Ven()

Local cCnt     := "1"
Local aStruct  := SA1->(dbStruct())
Local lContinua:= .T.
Static nVend
lPrim := .T.

If ( Empty(nVend) )
	nVend := 0
	While ( lContinua )
		If lPrim
			If ( aScan(aStruct,{|x| Trim(x[1]) == "A1_VEND"}) != 0 )
				nVend++
				cCnt := Soma1(cCnt,1)
			Else
				lContinua := .F.
			EndIf
			lPrim := .F.
		Else    
			If ( aScan(aStruct,{|x| Trim(x[1]) == "A1_VEND"+cCnt}) != 0 )
				nVend++
				cCnt := Soma1(cCnt,1)
			Else
				lContinua := .F.
			EndIf		
		EndIf
	EndDo
	nVend := Max(nVend,5)
EndIf
Return(nVend)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FE440Orige³ Autor ³---------------------- ³ Data ³--.--.--  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o Codigo de Origem do SE3                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 := Origem do SE3                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 := Programa de Chamada                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ FINE440                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Fe440Origem(cOrigem)

Local cRetorno := ""
cOrigem := AllTrim(cOrigem)
Do Case
Case cOrigem $ "FINA040#FINA280"
	cRetorno := "E" //Emissao Financeiro
Case cOrigem $ "FINA070#FINA330#FINA110#FINA087A"
	cRetorno := "B" //Baixa Financeiro
Case cOrigem $ "MATA460#MATA520#MATA467N#MATA465N#MATA468N"
	cRetorno := "F" //Faturamento
Case cOrigem $ "MATA100"
	cRetorno := "D" //Devolucao de Venda
Case cOrigem $ "FINE440"
	cRetorno := "R" //Recalculo quando nao ha origem
Case cOrigem $ "LOJA010#LOJA020#LOJA220#FRTA010#LOJA701"
	cRetorno := "L" //SigaLoja
OtherWise
	cRetorno := " " //Desconhecido
EndCase
Return(cRetorno)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ComisBx   ³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Responde com T ou F se uma comiss„o ser  considerada        ³±±
±±³          ³em fun‡„o de uma baixa                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ComixBx(ExpC1) - Motivo de uma baixa                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³FINE440                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function ComisBx( cMotBx )
	LOCAL nPos,lRet := .F.
	Local aMotBx := ReadMotBx()
	nPos := Ascan(aMotBx, {|x| Substr(x,1,3) == Upper(cMotBx) })
	If nPos > 0
		lRet := Iif(Substr(aMotBx[nPos],26,1) == "S",.T.,.F.)
	Endif
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³F440Loja  ³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Responde com T ou F se uma comiss„o foi gerada pelo SIGALOJA³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³F440Loja(ExpC1) - Sufixo (SE1 ou SE5)			  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso	     ³FINE440 							  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function f440Loja(cArquivo,cAlias)
Local aArea := GetArea()
LOCAL lRet := .F.

If Select("SL1") > 0
	If cArquivo == "SE1"
		dbSelectArea("SL1")
		dbSetOrder(2)	
		If MsSeek(xFilial(cAlias)+(cAlias)->E1_SERIE+(cAlias)->E1_NUM)
			lRet := .T.
		Endif
	Else
		dbSelectArea("SL1")
		dbSetOrder(2)
		If MsSeek(xFilial(cAlias)+(cAlias)->E5_PREFIXO+(cAlias)->E5_NUMERO)
			lRet := .T.
		Endif
	Endif
Endif
RestArea(aArea)
Return lRet			

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fe440DeleB³ Autor ³ --------------------- ³ Data ³ --.--.-- ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua descalculo da comissao na baixa                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aBaixas   : array c/ dados da baixa : [MOTBX,SEQ,REGISTRO] ³±±
±±³          ³ lJuros    : Considera Juros                                ³±±
±±³          ³ lDescont  : Considera Desconto                             ³±±
±±³          ³ cOrigem   : Origem da Comissao                             ³±±
±±³          ³ cVendDe   : Vendedor de ?                                  ³±±
±±³          ³ cVendAte  : Vendedor ate?                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINE440 - AGRICOPEL                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/																		
User Function fe440DeleB(aBaixas,lJuros,lDescont,cOrigem,cVendDe,cVendAte)

Local aArea 	:= GetArea()
Local aAreaSA3 := SA3->(GetArea())
Local aAreaSE1 := SE1->(GetArea())
Local aAreaSE5 := SE5->(GetArea())
Local aVendedor:= {}
Local cQuebra  := ""
Local cSeq     := ""
Local cTitulo  := ""
Local nCntFor  := 0
Local nCntFor2 := 0
Local cVend    := ""
Local cFatura  := ""
Local cVendedor:= ""
Local lFatura  := .F.
//Local nQtdVend := u_Fe440SA1Ven()
Local nQtdVend := u_fe440CntVen()
Local lDevolucao:= GetMv("MV_COMIDEV")
Local lCompensa := If(GetMv("MV_COMISCR")=="S",.T.,.F.)
Local lF440DelB := (Existblock("F440DELB"))

If ( Len(aBaixas) > 0 )
	dbSelectArea("SE3")
	aadd(aBaixas,{"zzz","zz",0}) // Controle de Saida
	For nCntFor := 1 To Len(aBaixas)
		If ( If(!lDevolucao,aBaixas[nCntFor,1]!="DEV",.T.) .And. ;
				If(!lCompensa ,aBaixas[nCntFor,1]!="CMP",.T.) )
			dbSelectArea("SE5")
			If ( aBaixas[nCntFor,3] != 0 )
				MsGoto(aBaixas[nCntFor,3])
			Else
				cQuebra := ""	
			EndIf	
			If ( cQuebra != aBaixas[nCntFor,1]+aBaixas[nCntFor,2] )
				If ( !Empty(cQuebra) .Or. aBaixas[nCntFor,3] == 0 )
					dbSelectArea("SE1")
					dbSetOrder(2)
					MsSeek(xFilial("SE1")+SE5->E5_CLIFOR+SE5->E5_LOJA+SE5->E5_PREFIXO+;
						SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO)
					lFatura   := If(SE1->E1_FATURA=="NOTFAT",.T.,.F.)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Adiciona vendedores que deverÆo ser descalculados  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( !lFatura )
						DbSelectArea("SA1")
						DbSetOrder(1)
						DbGotop()
						DbSeek(xFilial("SA1")+SE1->E1_CLIENTE+SE1->E1_LOJA)
						aVendedor := {}
						cVend := "1"
						lPrim := .T.
						For nCntFor2 := 1 to nQtdVend
							If lPrim 
								cVendedor := SA1->(FieldGet(FieldPos("A1_VEND")))
								lPrim := .F.
							Else
								cVendedor := SA1->(FieldGet(FieldPos("A1_VEND"+cVend)))
							EndIf	
							If ( !Empty(cVendedor) )
								AAdd( aVendedor , cVendedor)
							EndIf
							cVend := Soma1(cVend,1)
						Next nCntFor2
					Else
						aVendedor := {}
						cFatura := SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_PREFIXO+SE1->E1_NUM
						dbSelectArea("SE1")
						dbSetOrder(10)
						MsSeek(xFilial("SE1")+cFatura,.F.)
						While ( !Eof() .And. xFilial("SE1") == SE1->E1_FILIAL .And.;
								SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_FATPREF+SE1->E1_FATURA==;
								cFatura )
							cVend := "1"
							For nCntFor2 := 1 to nQtdVend
								cVendedor := SE1->(FieldGet(FieldPos("E1_VEND"+cVend)))
								If ( !Empty(cVendedor) )
									If ( aScan(aVendedor,{|x| x == cVendedor}) == 0 )
										AAdd( aVendedor , cVendedor)
									EndIf
								EndIf
								cVend := Soma1(cVend,1)
							Next nCntFor2
							dbSelectArea("SE1")
							dbSkip()
						EndDo
					EndIf
					For nCntFor2 := 1 To Len(aVendedor)
						If ( If(cVendDe==Nil.Or.cVendAte==Nil,.T.,;
								aVendedor[nCntFor2]>=cVendDe.And.;
								aVendedor[nCntFor2] <= cVendAte ) )
							dbSelectArea("SE3")
							dbSetOrder(3)
							MsSeek(xFilial("SE3")+aVendedor[nCntFor2]+cTitulo+cSeq,.F.)
							If ( Found() )
								If ( Empty(SE3->E3_DATA) ) // Nao esta pago
									RecLock("SE3")
									dbDelete()
									MsUnlock()
								Else
									u_Fe440CalcB(aBaixas,lJuros,lDescont,cOrigem,"-",aVendedor[nCntFor2],aVendedor[nCntFor2])
								EndIf
							Else
								If ( SE1->E1_SALDO != SE1->E1_VALOR )
									u_Fe440CalcB(aBaixas,lJuros,lDescont,cOrigem,"-",aVendedor[nCntFor2],aVendedor[nCntFor2])
								EndIf
							EndIf
						EndIf
						If lF440DelB
							ExecBlock("F440DELB",.F.,.F., aVendedor[nCntFor2])
						EndIf
					Next nCntFor2
					If aBaixas[nCntFor,3] == 0
						Exit
					EndIf
				EndIf
				cQuebra   := aBaixas[nCntFor,1]+aBaixas[nCntFor,2]
				cTitulo   := SE5->E5_CLIFOR+SE5->E5_LOJA+;
					SE5->E5_PREFIXO+SE5->E5_NUMERO+;
					SE5->E5_PARCELA+SE5->E5_TIPO
				cSeq		 := SE5->E5_SEQ
			EndIf
		EndIf
	Next nCntFor
EndIf
RestArea(aAreaSE5)
RestArea(aAreaSE1)
RestArea(aAreaSA3)
RestArea(aArea)
Return(.F.)
