#INCLUDE "FINA210.CH"
#INCLUDE "PROTHEUS.CH"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Fina210X ³ Autor ³ Wagner Xavier         ³ Data ³ 01.12.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Relcula os saldos bancarios dentro de um determinado       ³±±
±±³          ³ periodo                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FinA210X(void)                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³			ATUALIZACOES SOFRIDAS                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Claudio  ³13/07/00³xxxxxx³ Retirar todas as chamadas a WriteSx2       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function FinA210X()

LOCAL nOpca	:=0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL oDlg, aSays:={}, aButtons:={}
Private cCadastro := OemToAnsi(STR0004)  //"Reconcilia‡„o de Saldos Banc rios"

Pergunte("FIN210",.F.)
AADD (aSays, OemToAnsi(STR0005))  //"  Este programa tem como objetivo recalcular e analisar os saldos   "
AADD (aSays, OemToAnsi(STR0006))  //"Banc rios dia a dia de um determinado per¡odo at‚ a data base do    "
AADD (aSays, OemToAnsi(STR0007))  //"sistema. Utilizando no caso de haver necessidade de retroagir a     "
AADD (aSays, OemToAnsi(STR0008))  //"movimenta‡„o banc ria. Use como referˆncia a data em que o saldo    "
AADD (aSays, OemToAnsi(STR0009))  //"ficou defasado. "

AADD(aButtons, { 1,.T.,{|o| nOpca:= 1,o:oWnd:End()}} )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
AADD(aButtons, { 5,.T.,{|| Pergunte("FIN210",.T. ) } } )
FormBatch( cCadastro, aSays, aButtons )
If nOpcA == 1
	#IFDEF TOP
		If TcSrvType() == 'AS/400'	
			Processa({|lEnd| FA210Processa()})  // Chamada da funcao de reconcilia‡Æo
		Else
			Processa({|lEnd| FA211Processa()})  // Chamada da funcao de reconcilia‡Æo
		Endif
	#ELSE
		Processa({|lEnd| FA210Processa()})  // Chamada da funcao de reconcilia‡Æo
	#ENDIF
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Fa210Pro ³ Autor ³ Wagner Xavier         ³ Data ³ 01.12.92 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recalcula os saldos bancarios dentro de um determinado     ³±±
±±³          ³ periodo                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fa210Processa()                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function FA210Processa()

LOCAL nSaldoIni
LOCAL nEntradas
LOCAL nSaidas, nData, cCond
LOCAL dDataMovto
LOCAL cFil    :=""
LOCAL lAllFil :=.F.
LOCAL cChave
LOCAL cIndex  := ""
LOCAL lSaida  :=.F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // Do Banco                                         ³
//³ mv_par02 // Ate o Banco                                      ³
//³ mv_par03 // Da Agˆncia                                       ³
//³ mv_par04 // At‚ a Agˆncia                                    ³
//³ mv_par05 // Da Conta                                         ³
//³ mv_par06 // At‚ a Conta                                      ³
//³ mv_par07 // A partir da Data                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea( "SA6" )
dbSeek( cFilial+mv_par01 , .T.)

If !DtMovFin(mv_par07,.F.)
	Help(" ",1,"DATAFIN",,STR0016+; //"Nao sao permitidos recalculos de saldos bancarios"
										STR0017,1,0) //"de data anterior a data contida no parametro MV_DATAFIN"
	Return
Endif

ProcRegua(RecCount())

If Empty(xFilial( "SA6")) .AND. !Empty(xFilial("SE5"))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtra o arquivo por tipo e vencimento                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SE5")
	cIndex 	:= CriaTrab(nil,.f.)
	cChave  	:= "E5_BANCO+E5_AGENCIA+E5_CONTA+DTOS(E5_DTDISPO)"
	cCond		:= 'dtos(E5_DTDISPO)>="'+dtos(mv_par07)+'"'
	IndRegua("SE5",cIndex,cChave,,cCond,OemToAnsi(STR0015))  //"Selecionando Registros..."
	nIndexSE5 := RetIndex("SE5")
	#IFNDEF TOP
		dbSetIndex(cIndex+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndexSE5+1)
	lSaida 	:= .T.
	dbGoTop()
	lAllFil:= .T.
Else
	dbSelectArea("SE5")
	cIndex 	:= CriaTrab(nil,.f.)
	cChave  	:= "E5_FILIAL+E5_BANCO+E5_AGENCIA+E5_CONTA+DTOS(E5_DTDISPO)"
	cCond		:= 'dtos(E5_DTDISPO)>="'+dtos(mv_par07)+'" .and. E5_FILIAL == "'+xFilial("SE5")+'"'
	IndRegua("SE5",cIndex,cChave,,cCond,OemToAnsi(STR0015))  //"Selecionando Registros..."
	nIndexSE5 := RetIndex("SE5")
	#IFNDEF TOP
		dbSetIndex(cIndex+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndexSE5+1)
	dbGoTop()	
Endif	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia rec lculo dos saldos, atrav‚s da movimenta‡„o banc ria³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "SA6" )
dbSeek( cFilial+mv_par01 , .T.)

While !Eof() .and. A6_FILIAL == cFilial .and. A6_COD <= mv_par02

	IncProc()

	//Alteracao para nao recalcular o saldo dos caixas do Loja, pois
	//estes devem ser recalculados atraves da opcao "Recalculo de Caixa" - Adriano
	dbSelectArea("SX5")
	If (dbSeek(xFilial("SX5")+"23"+SA6->A6_COD)) .or. (SA6->A6_Cod == "CL1")
		dbSelectArea("SA6")
		dbSkip()
		Loop
	Endif

	dbSelectArea("SA6")
	cBanco   := A6_COD
	cAgencia := A6_AGENCIA
	cConta   := A6_NUMCON
	nSaldoIni:= 0
	nEntradas:= 0
	nSaidas  := 0

	If cAgencia < mv_par03 .or. cAgencia > mv_par04 .or. cConta < mv_par05 .or. cConta > mv_par06
		dbSkip( )
		Loop
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza Saldo de Partida.                              ³
	//³ Observe que o programa retorna um registro no banco de  ³
	//³ dados, portanto a data de referencia ‚  a data em que   ³
	//³ o saldo ficou errado, n„o a data correta do saldo.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SE8" )
	dbSeek( cFilial+cBanco+cAgencia+cConta+Dtos(mv_par07),.T. )
	dbSkip( -1 )
	If  E8_BANCO != cBanco .or. E8_AGENCIA != cAgencia .or. E8_CONTA != cConta .or. BOF() .or. EOF()
		nSaldoIni := 0
	Else
		nSaldoIni := E8_SALATUA
	End
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza movimenta‡„o banc ria                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SE5" )
	dbSetOrder(nIndexSE5+1)
	cFil := Iif(lAllFil,"",xFilial("SE5"))
	dbSeek(cFil+cBanco+cAgencia+cConta+Dtos(mv_par07),.T.)
	While !Eof() .and. E5_BANCO+E5_AGENCIA+E5_CONTA == cBanco+cAgencia+cConta
		IF !lAllFil .and. E5_FILIAL != xFilial("SE5")
			Exit
		Endif	
		dDataMovto := E5_DTDISPO
		While !Eof() .and. E5_BANCO+E5_AGENCIA+E5_CONTA+dtos(E5_DTDISPO) == cBanco+cAgencia+cConta+dtos(dDataMovto)
			IF !lAllFil .and. E5_FILIAL != xFilial("SE5")
				Exit
			Endif	

			IF E5_TIPODOC $ "DC/JR/MT/CM/D2/J2/M2/C2/V2/CP/TL"  //Valores de Baixas
				dbSkip()
				Loop
			Endif
			*
			* Desconsidera Cheque troco no calculo
			*
			IF E5_TIPODOC == "CA"  // Cheque Avulso/Troco
				dbSkip()
				Loop
			Endif

			If E5_VENCTO > E5_DATA      // Ignora pre' datados - gerou titulo
				dbSkip()
				Loop
			Endif

			If E5_SITUACA = "C"     //Cancelado
				dbSkip()
				Loop
			Endif

			If SE5->E5_MOEDA $ "C1/C2/C3/C4/C5/CH" .and. Empty(SE5->E5_NUMCHEQ) .and. !(E5_TIPODOC $ "TR#TE")
				dbSkip()
				Loop
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Na transferencia somente considera nestes numerarios 		  ³
			//³ No Fina100 ‚ tratado desta forma.                    		  ³
			//³ As transferencias TR de titulos p/ Desconto/Cau‡Æo (FINA060) ³
			//³ nÆo sofrem mesmo tratamento dos TR bancarias do FINA100      ³
            //³ Aclaracao : Foi incluido o tipo $ para os movimentos en di-- ³
            //³ nheiro em QUALQUER moeda, pois o R$ nao e representativo     ³
            //³ fora do BRASIL. Bruno 07/12/2000 Paraguai                    ³
            //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If SE5->E5_TIPODOC $ "TR/TE" .and. Empty(SE5->E5_NUMERO)
            If !(E5_MOEDA $ " $ /R$/DO/TB/TC/CH"+IIf(cPaisLoc=="BRA","","/$ "))
					dbSkip()
					Loop
				Endif
			Endif

			If E5_TIPODOC $ "TR/TE" .and. (Substr(E5_NUMCHEQ,1,1)=="*" ;
				.or. Substr(E5_DOCUMEN,1,1) == "*" )
				dbSkip()
				Loop
			Endif

			If SE5->E5_MOEDA == "CH" .and. IsCaixaLoja(SE5->E5_BANCO)		//Sangria
				dbSkip()
				Loop
			Endif

			If SubStr(E5_NUMCHEQ,1,1)=="*"      //cheque para juntar (PA)
				dbSkip()
				Loop
			Endif

			If !Empty(SE5->E5_MOTBX)
				If !MovBcoBx(SE5->E5_MOTBX)
					dbSkip()
					Loop
				Endif
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Baixa autom tica 							                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IF E5_TIPODOC = "BA" 
				dbSkip()
				Loop
			End

			dbSelectArea("SE5")
			IF  E5_RECPAG = "R"
				nEntradas += E5_VALOR
			Else
				nSaidas += E5_VALOR
			End
			dbSkip()
		End  // Quebra da data
		dbSelectArea("SE8")
		dbSeek(cFilial + cBanco+cAgencia+cConta+Dtos(dDataMovto))
		IF Eof()
			RecLock("SE8",.t.)
		Else
			RecLock("SE8",.f.)
		Endif
		Replace 	E8_FILIAL   With cFilial,;
					E8_BANCO    With cBanco,;
					E8_AGENCIA  With cAgencia,;
					E8_CONTA    With cConta,;
					E8_DTSALATU With dDataMovto
					Replace E8_SALATUA With nSaldoIni+nEntradas-nSaidas
		MsUnlock()
		dbSelectArea("SE5")
		IF !(SE5->(Eof())) .and. E5_BANCO+E5_AGENCIA+E5_CONTA == cBanco+cAgencia+cConta 
			IF !lAllFil .and. E5_FILIAL != xFIlial("SE5")
				Exit
			Endif
			For nData := dDataMovto+1 to ((SE5->E5_DTDISPO) - 1)
				dbSelectArea("SE8")
				If	dbSeek(cFilial + cBanco+cAgencia+cConta+dtos(nData))
					RecLock("SE8",.F.,.T.)
					dbDelete()
					MsUnlock()
					SX2->(MsUnlock())
				EndIf	
				dbSelectArea("SE5")				
			Next
		Endif
		IF SE5->(Eof()) .or. E5_BANCO+E5_AGENCIA+E5_CONTA != cBanco+cAgencia+cConta
			dbSelectArea("SE8")
			dbSeek(cFilial + cBanco+cAgencia+cConta+dtos(dDataMovto+1),.t.) 
			While !Eof() .and. cFilial + cBanco+cAgencia+cConta == E8_FILIAL+E8_BANCO+E8_AGENCIA+E8_CONTA
					RecLock("SE8",.F.,.T.)
					dbDelete()
					MsUnlock()
					SX2->(MsUnlock())
					dbselectArea("SE8")
					dbSkip()
			End
		End
		dbSelectArea("SE5")
	End  // Fecha Primeiro Loop do SE5
	dbSelectArea("SA6")
	RecLock("SA6")
	Replace A6_SALATU With nSaldoIni+nEntradas-nSaidas		 	
	MsUnLock()
	dbSkip()
End
dbSelectArea("SE5") 
RetIndex( "SE5")
Set Filter To
If !Empty(cIndex)
	ferase( cIndex+OrdBagExt())
EndIf	
Return NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Fa211Pro ³ Autor ³ Wagner Xavier         ³ Data ³ 09.03.00 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Recalcula os saldos bancarios dentro de um determinado     ³±±
±±³          ³ periodo para versao TopConnect                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Fa211Processa()                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
#IFDEF TOP
Static Function FA211Processa()

LOCAL nSaldoIni, nEntradas
LOCAL nSaidas, nData
LOCAL cQuery
LOCAL dDataMovto

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // Do Banco                                         ³
//³ mv_par02 // Ate o Banco                                      ³
//³ mv_par03 // Da Agˆncia                                       ³
//³ mv_par04 // At‚ a Agˆncia                                    ³
//³ mv_par05 // Da Conta                                         ³
//³ mv_par06 // At‚ a Conta                                      ³
//³ mv_par07 // A partir da Data                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ProcRegua(RecCount())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia rec lculo dos saldos, atrav‚s da movimenta‡„o banc ria³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery := "SELECT SA6.R_E_C_N_O_ A6_RECNO,"
cQuery += " E5_BANCO, E5_AGENCIA, E5_CONTA, E5_DTDISPO, E5_TIPODOC, E5_MOEDA,"
cQuery += " E5_NUMCHEQ, E5_MOTBX, E5_NUMERO, E5_RECPAG, E5_VALOR, E5_DOCUMEN"
cQuery += " FROM " + RetSqlName("SA6") + " SA6, " + RetSqlName("SE5") + " SE5"
cQuery += " WHERE A6_FILIAL = '" + xFilial("SA6") + "'"
cQuery += " AND   A6_COD     between '" + mv_par01 + "' AND '" + mv_par02 + "'"
cQuery += " AND   A6_AGENCIA between '" + mv_par03 + "' AND '" + mv_par04 + "'"
cQuery += " AND   A6_NUMCON  between '" + mv_par05 + "' AND '" + mv_par06 + "'"
cQuery += " AND   SA6.D_E_L_E_T_ <> '*'"
cQuery += " AND   E5_VENCTO <= E5_DATA"
cQuery += " AND   A6_COD     = E5_BANCO"
cQuery += " AND   A6_AGENCIA = E5_AGENCIA"
cQuery += " AND   A6_NUMCON  = E5_CONTA"
cQuery += " AND   E5_SITUACA <> 'C'"
cQuery += " AND   E5_TIPODOC <> 'BA'" 
cQuery += " AND   SE5.D_E_L_E_T_ <> '*'"

If Empty(xFilial( "SA6")) .AND. !Empty(xFilial("SE5"))
	cQuery += " AND E5_DTDISPO >= '" + dtos(mv_par07) + "'"
	//O filtro de filial entre ' ' e 'ZZ' foi adicionado para o SQL utilizar indice
	cQuery += " AND E5_FILIAL between '  ' AND 'ZZ'"
Else
	cQuery += " AND E5_DTDISPO >= '" + dtos(mv_par07) + "' AND E5_FILIAL = '" + xFilial("SE5") + "'"
Endif	

cQuery += " ORDER BY E5_BANCO, E5_AGENCIA, E5_CONTA, E5_DTDISPO"
	
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),'TRB210',.T.,.T.)
TcSetField("TRB210", "E5_DTDISPO", "D")

While !Eof()

	dbSelectArea("TRB210")
	cBanco   := E5_BANCO
	cAgencia := E5_AGENCIA
	cConta   := E5_CONTA
	nSaldoIni:= 0
	nEntradas:= 0
	nSaidas  := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza Saldo de Partida.                              ³
	//³ Observe que o programa retorna um registro no banco de  ³
	//³ dados, portanto a data de referencia ‚  a data em que   ³
	//³ o saldo ficou errado, n„o a data correta do saldo.      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SE8" )
	dbSeek( cFilial+cBanco+cAgencia+cConta+Dtos(mv_par07),.T. )
	dbSkip( -1 )
	If  E8_BANCO != cBanco .or. E8_AGENCIA != cAgencia .or. E8_CONTA != cConta .or. BOF() .or. EOF()
		nSaldoIni := 0
	Else
		nSaldoIni := E8_SALATUA
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza movimenta‡„o banc ria                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("TRB210")
	While !Eof() .and. E5_BANCO+E5_AGENCIA+E5_CONTA == cBanco+cAgencia+cConta
		dDataMovto := E5_DTDISPO
		While !Eof() .and. E5_BANCO+E5_AGENCIA+E5_CONTA+DTOS(E5_DTDISPO) == cBanco+cAgencia+cConta+dtos(dDataMovto)

			IF E5_TIPODOC $ "DC/JR/MT/CM/D2/J2/M2/C2/V2/CP/TL"  //Valores de Baixas
				dbSkip()
				Loop
			End
			*
			* Desconsidera Cheque troco no calculo
			*
			IF E5_TIPODOC == "CA"  // Cheque Avulso/Troco
				dbSkip()
				Loop
			Endif

			If E5_MOEDA $ "C1/C2/C3/C4/C5/CH" .and. Empty(E5_NUMCHEQ) .and. !(E5_TIPODOC $ "TR#TE")
				dbSkip()
				Loop
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Na transferencia somente considera nestes numerarios 		  ³
			//³ No Fina100 ‚ tratado desta forma.                    		  ³
			//³ As transferencias TR de titulos p/ Desconto/Cau‡Æo (FINA060) ³
			//³ nÆo sofrem mesmo tratamento dos TR bancarias do FINA100      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If E5_TIPODOC $ "TR/TE" .and. Empty(E5_NUMERO)
				If !(E5_MOEDA $ "R$/DO/TB/TC/CH"+IIf(cPaisLoc=="BRA","","/$ "))
					dbSkip()
					Loop
				Endif
			Endif

			If E5_TIPODOC $ "TR/TE" .and. (Substr(E5_NUMCHEQ,1,1)=="*" ;
				.or. Substr(E5_DOCUMEN,1,1) == "*" )
				dbSkip()
				Loop
			Endif

			If E5_MOEDA == "CH" .and. IsCaixaLoja(E5_BANCO)		//Sangria
				dbSkip()
				Loop
			Endif

			If SubStr(E5_NUMCHEQ,1,1)=="*"      //cheque para juntar (PA)
				dbSkip()
				Loop
			Endif

			If !Empty(E5_MOTBX)
				If !MovBcoBx(E5_MOTBX)
					dbSkip()
					Loop
				Endif
			Endif

			If E5_RECPAG = "R"
				nEntradas += E5_VALOR
			Else
				nSaidas += E5_VALOR
			End
			dbSkip()
		End  // Quebra da data

		dbSelectArea("SE8")
		dbSeek(cFilial+cBanco+cAgencia+cConta+Dtos(dDataMovto))
		IF Eof()
			RecLock("SE8",.t.)
		Else
			RecLock("SE8",.f.)
		Endif
		Replace 	E8_FILIAL   With cFilial,;
					E8_BANCO    With cBanco,;
					E8_AGENCIA  With cAgencia,;
					E8_CONTA    With cConta,;
					E8_DTSALATU With dDataMovto
					Replace E8_SALATUA With nSaldoIni+nEntradas-nSaidas
		MsUnlock()

		dbSelectArea("TRB210")
		IF !(TRB210->(Eof())) .and. E5_BANCO+E5_AGENCIA+E5_CONTA == cBanco+cAgencia+cConta 
			For nData := dDataMovto+1 to ((E5_DTDISPO) - 1)
				dbSelectArea("SE8")
				If	dbSeek(cFilial + cBanco+cAgencia+cConta+dtos(nData))
					RecLock("SE8",.F.,.T.)
					dbDelete()
					MsUnlock()
					SX2->(MsUnlock())
				EndIf	
				dbSelectArea("TRB210")
			Next
		Endif

		If TRB210->(Eof()) .or. E5_BANCO+E5_AGENCIA+E5_CONTA != cBanco+cAgencia+cConta
			dbSelectArea("SE8")
			dbSeek(cFilial+cBanco+cAgencia+cConta+dtos(dDataMovto+1),.t.) 
			While !Eof() .and. cFilial+cBanco+cAgencia+cConta == E8_FILIAL+E8_BANCO+E8_AGENCIA+E8_CONTA
				RecLock("SE8",.F.,.T.)
				dbDelete()
				MsUnlock()
				SX2->(MsUnlock())
				dbselectArea("SE8")
				dbSkip()
			Enddo
		Endif
		dbSelectArea("TRB210")
	Enddo  // Fecha Primeiro Loop do SE5

	dbSelectArea("SA6")
	If dbSeek( xFilial("SA6")+cBanco+cAgencia+cConta)
		RecLock("SA6")
		Replace A6_SALATU With nSaldoIni+nEntradas-nSaidas
		MsUnLock()
	EndIf
	dbSelectArea("TRB210") 
Enddo
dbSelectArea("TRB210") 
dbCloseArea()
Return NIL
#ENDIF
